###############################################################################
#
###############################################################################

DELIMITERS = "<.>" "<..>" "<...>" "<!>" "<?>" "<¶>" ;
SOFT-DELIMITERS = "<,>" ;
SUBREADINGS = LTR ;
#OPTIONS += no-inline-sets no-inline-templates ;

LIST BOS = (>>>) ;
LIST EOS = (<<<) ;

LIST N =  n N ;
LIST Prop =  np Prop ;
LIST Pr =  pr Pr ;
LIST Pron =  prn Pron ;
LIST Det =  det Det ;
LIST Num =  num Num "<[0-9]+>"r ("млн" abbr) ("тыс" abbr)  ("млн" Abbr) ("тыс" Abbr)  ;
LIST A =  adj A ;
LIST Adv =  adv Adv ;
LIST CS =  cnjsub CS ;
LIST CC =  cnjcoo CC ;
LIST Interj =  ij Interj ;
LIST V =  vblex vbser vbmod V ;
LIST Pred =  pred Pred ;
LIST Part =  part Part ;
LIST Abbr =  abbr Abbr ;
LIST Poss = poss Poss ;
LIST Ref = ref Ref ;

LIST Pass =  pass pasv Pass ;  # Check
LIST V/Pass =  pass V/Pass ;
LIST Actv = actv Actv ;

LIST Lxc = lxc Lxc ; #lexicalized participial forms
LIST Der = der Der; #readings assigned to all participial wordforms presupposing their ambiguity

LIST Cop =  ("быть" vblex)  ("быть" V)  ;

LIST Guio =  guio "-"  ;
LIST SafeGuio =  "—"  ;
LIST Cm =  cm  ;
LIST Excl =  ("!")  ;
LIST Ques =  ("?")  ;
LIST Sent =  sent  ;
LIST Comm = "<,>" ;
LIST Dash ="<->";
LIST QuotPlain = "<">";
LIST Col = "<:>" ;
LIST SemCol = "<;>" ;

LIST Percent =  ("<[0-9]+%>"r) percent  ;
LIST Roman =  roman ("<M{0,4}(CM|CD|D(C{0,3}))?(XC|XL|L(X{0,3}))?(IX|IV|V(I{0,3}))?>"ri num) ("<[MCDXLIVМСХ]+>"ri num)  ("<M{0,4}(CM|CD|D(C{0,3}))?(XC|XL|L(X{0,3}))?(IX|IV|V(I{0,3}))?>"ri Num) ("<[MCDXLIVМСХ]+>"ri Num)  ;
LIST Count =  count Count ;
#LIST Cipher =  "<[0-9]+>"r  ;
LIST Cipher =  "[0-9]+"r  ;

LIST Pprep =  pprep pobj Pprep ;
LIST Pers =  pers Pers ;
LIST Paren =  paren Paren ;

LIST Lparen =  lpar Lparen ;
LIST Parens =  lpar rpar par Parens ;
LIST Apos =  apos Apos ;
LIST Quot =  lquot rquot quot Quot ;

LIST Comp = Comp (comp pred)  ;
LIST Short = Short short (adj m pred) (adj f pred) (adj mfn pred) (adj nt pred)  ; # pred
LIST Cmp = Cmp cmp  ;

LIST PrcPast = PrcPast pp  ;
LIST PrcPres = PrcPres pprs  ;

LIST PrsAct = prsact PrsAct  ;
LIST PstAct = pstact PstAct  ;
LIST PrsPss = prspss PrsPss  ;
LIST PstPss = pstpss PstPss  ;

SET PastPass = PrcPast + Pass ; 

SET PresPass = PrcPres + Pass ;

SET PastActv = PrcPast + Actv ;

SET PresActv = PrcPres + Actv ;

SET Prc =  PrcPast | PrcPres ;

LIST TV = TV tv  ;
LIST IV = IV iv  ;

LIST Ant = Ant ant  ;
LIST Cog = Cog cog  ;
LIST Al = Al al  ;  # includes Top

LIST Msc = Msc m  ;
LIST Fem = Fem f  ;
LIST Neu = Neu nt  ;

LIST Sg = Sg sg  ;
LIST Pl = Pl pl  ;

LIST Sg1 = Sg1 (p1 sg)  ;
LIST Sg2 = Sg2 (p2 sg)  ;
LIST Sg3 = Sg3 (p3 sg)  ;
LIST Pl1 = Pl1 (p1 pl)  ;
LIST Pl2 = Pl2 (p2 pl)  ;
LIST Pl3 = Pl3 (p3 pl)  ;

LIST Impf = Impf impf  ;
LIST Perf = Perf perf  ;

LIST Ord = Ord ord  ;
LIST Dem = Dem dem  ;
LIST Interr = Interr itg  ;
LIST Rel = Rel rel  ;

LIST DetIndecl = DetIndecl ("её" det pos) ("его" det pos) ("их" det pos)  ;

LIST Nom = Nom nom  ;
LIST Gen = Gen gen  ;
LIST Dat = Dat dat  ;
LIST Acc = Acc acc  ;
LIST Ins = Ins ins  ;
LIST Prp = Prp prp  ;

LIST Loc = Loc loc  ;
LIST Par = Par par  ;

LIST Anim = Anim aa an  ;
LIST Inanim = Inanim nn an  ;

LIST Inf = Inf inf  ;
LIST Imper = Imper imp  ;
LIST Past = Past past  ;
LIST Fin = Past Pres Fut past pres fut  ;

LIST Err/L2_ii = Err/L2_ii ;
LIST Err/L2_revIkn = Err/L2_revIkn ;

LIST Prn/Sebe =  ("<себе>"i prn)  ("<себе>"i Pron)  ;
LIST Prn/Tebe =  ("<тебе>"i prn)  ("<тебе>"i Pron)  ;
LIST Prn/Komu =  ("<кому>"i prn)  ("<кому>"i Pron)  ;
LIST Prn/Tot =  ("тот"i prn)  ("<тот>"i Pron)  ;
LIST Prn/To =  ("то"i prn)  ("<то>"i Pron)  ;
LIST Prn/Togo =  ("<того>"i prn)  ("<того>"i Pron)  ;

LIST Num/Neskolko =  ("несколько" num)  ("несколько" Num)  ;
LIST Num/Tri =  ("три" num)  ("три" Num)  ;

LIST CS/Čto =  ("что²" cnjsub)  ("что²" CS)   ;
LIST CC/I =  ("и" cnjcoo)  ;
LIST CC/A =  ("а" cnjcoo)  ;

LIST Paren/Konečno =  ("конечно" paren)  ("конечно" Paren)  ;

LIST Jest =  ("<есть>"i)  ;

LIST Prop/Maša =  ("Маша" np)  ("Маша" N Prop)  ;

LIST Pr/V = ("в" pr) ("в" Pr) ;
LIST Pr/Na = ("на" pr) ("на" Pr) ;
LIST Pr/S = ("с" pr) ("с" Pr) ;
LIST Pr/Po = ("по" pr) ("по" Pr) ;
LIST Pr/Iz = ("из" pr) ("из" Pr) ;
LIST Pr/K = ("к" pr) ("к" Pr) ;
LIST Pr/Ot = ("от" pr) ("от" Pr) ;
LIST Pr/Dlja = ("для" pr) ("для" Pr) ;
LIST Pr/Do = ("до" pr) ("до" Pr) ;
LIST Pr/Za = ("за" pr) ("за" Pr) ;
LIST Pr/O = ("о" pr) ("о" Pr) ;
LIST Pr/Pri = ("при" pr) ("при" Pr) ;
LIST Pr/U = ("у" pr) ("у" Pr) ;
LIST Pr/Posle = ("после" pr) ("после" Pr) ;
LIST Pr/Pod = ("<под>"i pr) ("<под>"i Pr) ;
LIST Pr/Meždu = ("между" pr) ("между" Pr) ;
LIST Pr/Okolo = ("около" pr) ("около" Pr) ;
LIST Pr/Čerez = ("через" pr) ("через" Pr) ;
LIST Pr/Protiv = ("против" pr) ("против" Pr) ;
LIST Pr/Naprotiv = ("напротив" pr) ("напротив" Pr) ;
LIST Pr/Nad = ("над" pr) ("над" Pr) ;
LIST Pr/Bez = ("без" pr) ("без" Pr) ;
LIST Pr/Sredi = ("среди" pr) ("среди" Pr) ;
LIST Pr/Iz-za = ("из-за" pr) ("из-за" Pr) ;
LIST Pr/Pered = ("перед" pr) ("перед" Pr) ;
LIST Pr/Blagodarja = ("благодаря" pr) ("благодаря" Pr) ;
LIST Pr/Krome = ("кроме" pr) ("кроме" Pr) ;
LIST Pr/Vključaja = ("включая" pr) ("включая" Pr) ;
LIST Pr/Soglasno = ("согласно" pr) ("согласно" Pr) ;
LIST Pr/Pomimo = ("помимо" pr) ("помимо" Pr);
LIST Pr/Vnutri = ("внутри" pr) ("внутри" Pr) ;

LIST Pr/Nado = ("<надо>"i pr) ("<надо>"i Pr) ;

LIST N/Filma = ("фильма" n) ("фильма" N) ;
LIST N/Film = ("фильм" n) ("фильм" N) ;
LIST N/Bukva = ("буква" n) ("буква" N) ;
LIST N/Goda = ("<года>"i n) ("<года>"i N) ;
LIST N/Godah = ("<годах>"i n) ("<годах>"i N) ;
LIST N/Potom = ("<потом>"i n) ("<потом>"i N) ;
LIST N/Krov = ("кровь" n) ("кровь" N) ;
LIST N/Tomu = ("<тому>"i n) ("<тому>"i N) ;
LIST N/Golova = ("голова" n) ("голова" N) ;
LIST N/Denga = ("деньга" n) ("деньга" N) ;
LIST N/Voda = ("<вода>"i n) ("<вода>"i N) ;
LIST N/Vode = ("<водe>"i n) ("<водe>"i N) ;
LIST N/Beregu = ("<берегу>"i n) ("<берегу>"i N) ;
LIST N/Vek = ("век¹" n) ("век¹" N);
LIST N/Veko = ("веко" n) ("веко" N) ;
LIST N/Vtora = ("<второй>"i n) ("<второй>"i N);
LIST N/Mat = ("мать" n) ("мать" N) ;
LIST N/Mater = ("матерь" n) ("матерь" N) ;
LIST N/Pomoč = ("помочь" n) ("помочь" N) ;
LIST N/Polku = ("<полку>"i n) ("<полку>"i N);
LIST N/Mire = ("<мире>"i n) ("<мире>"i N);
LIST N/Miro = ("миро" n) ("миро" N);
LIST N/Metr = ("метр" n) ("километр" n) ("метр" N) ("километр" n);
LIST N/Primer = ("пример" n) ("пример" N);
LIST N/Delo = ("дело" n) ("дело" N) ;
LIST N/Pravilo¹ = ("правило¹" n) ("правило¹" N) ;
LIST N/Pravilo² = ("правило²" n) ("правило²" N) ;
LIST N/Noga = ("нога" n) ("нога" N) ;
LIST N/Lob = ("лоб" n) ("лоб" N) ;
LIST N/Byli = ("<были>"i n) ("<были>"i N) ;
LIST N/On = ("он²" n) ("он²" N) ;
LIST N/Stradanje = ("страданье" n) ("страданье" N) ;
LIST N/Stradanie = ("страдание" n) ("страдание" N) ;
LIST N/Slony = ("слоны" n) ("слоны" N) ;
LIST N/Denj = ("день" n) ("день" N) ;
LIST N/Čelovek = ("человек" n) ("человек" N) ;
LIST N/Ljudi = ("люди²" n) ("люди²" N) ;
LIST N/Maj = ("май" n) ("май" N) ;

LIST A/Malo = ("<мало>"i adj) ("<мало>"i A) ;
LIST A/Božij = ("божий" adj) ("божий" A) ;
LIST A/Metrov = ("<метров>"i adj) ("<километров>"i adj) ("<метров>"i A) ("<километров>"i adj) ;
LIST A/Prišlij = ("пришлый" adj) ("пришлый" A) ;
LIST A/Boevoj = ("боевой" adj) ("боевой" A) ;

LIST Adv/I = ("и" adv) ("и" Adv) ;
LIST Adv/Ješë = ("ещё" adv) ("ещё" Adv) ;
LIST Adv/Krugom² = ("кругом²" adv) ("кругом²" Adv) ;
LIST Adv/Celikom = ("целиком" adv) ("целиком" Adv) ;
LIST Adv/Posle = ("после" adv) ("после" Adv) ;
LIST Adv/Neskolko = ("несколько" adv) ("несколько" Adv) ;
LIST Adv/Cmpar = ("более" adv) ("более" Adv) ("менее" adv) ("менее" Adv);

LIST V/Oblivatsja = ("обливаться" vblex) ("обливаться" V) ;
LIST V/Pahnut = ("пахнуть" vblex) ("пахнуть" V) ;
LIST V/Isteč = ("истечь" vblex) ("истечь" V) ;
LIST V/Vypuskat = ("выпускать" vblex) ("выпускать" V) ;
LIST V/Takaja = ("<такая>"i vblex) ("<такая>"i V) ;
LIST V/Primer = ("<пример>"i vblex) ("<пример>"i V) ;
LIST V/Dela = ("<дела>"i vblex) ("<дела>"i V) ;
LIST V/Pisat = ("писать" vblex) ("написать" vblex) ("описать" vblex) ("писать" V) ("написать" vblex) ("описать" vblex) ;
LIST V/Prijti = ("прийти" vblex) ("прийти" V) ;
LIST V/Stroit = ("строит" vblex) ("строит" V) ;
LIST V/Vyhodit = ("выходить" vblex) ("выходить¹⁻²" vblex) ("выходить" V) ("выходить¹⁻²" vblex) ;
LIST V/Idjot = ("идти" vblex pres) ("идти" V Pres) ;
LIST V/Byt = ("быть" vblex) ("быть" V) ;
#LIST V/Byt = ("мочь" vblex) ("мочь" V) ;

LIST Ij/Na = ("на" ij) ("на" Interj) ;
LIST Ij/Ih = ("их²" ij) ("их²" Interj) ;
LIST Ij/I = ("и¹" ij) ("и¹" Interj) ;
LIST Ij/A = ("а¹" ij) ("а¹" Interj) ;
LIST Ij/No = ("но²" ij) ("но²" Interj) ;

LIST O = "о" ;
LIST Ja = "я" "я¹" "я²" ;
LIST I = "и" ;
LIST No = "но" ;
LIST Ne = "не" ;
LIST Samyj = "самый" ;
LIST Naibolee = "наиболее" ;
LIST Kotoryj = "который" ;
LIST Posle = "после" ;
LIST Čto = "что¹" "что²" ;
LIST Kto = "кто" ;
LIST Nazad = "назад" ;
LIST Sliškom = "слишком" ;
LIST Našej = "<нашей>"i ;
LIST Sej = "сей" ;
LIST Takže = "также" ;
LIST Tože = "тоже" ;
LIST Tak = "так" ;
LIST Kak = "как" ;
LIST Tak_I = "так и" ;
LIST Sam = "сам" ;
LIST Bylo = "<было>"i ;
LIST U = "у¹" "у²" "у" ;
LIST Vo = "<во>"i ;
LIST Ili = "или" ;
LIST Že = "же" ;
LIST To = "то" ;
LIST Eto = "это" ;
LIST Oneletterp = "с" "в" "к";

LIST PrefixNado = "<[бвгджзйклмнпрстуфхцчшщъь][бвгджзйклмнпрстуфхцчшщъь].*>"r ;

LIST NumNotNom = "<([0-9])*[023456789]>"r "<([0-9])*11>"r ;

LIST Unknown = "\*.*"r ;

LIST LesserNum = "полтора" "два" "три" "четыре" "обо" "<([2-90]+)*[1-4]>"r ;

LIST Poltora = "полтора" ;
LIST Pjat = "пять" "5" "<([0-9])*5>"r ;
LIST Sto = "сто" "100" ;
LIST Tysjača = "тысяча" "1000" ;

LIST Day = "день" ;
LIST Month = "месяц" ;
LIST Months = "январь" "февраль" "март" "апрель" "май" "июнь" "июль" "август" "сентябрь" "октябрь" "ноябрь" "декабрь" ;
LIST Year = "год" "год¹" ("г." abbr) ;
LIST Century = "век" ;
LIST Season = "сезон" ;
LIST Seasons = "весна" "лето" "осень" "зима" ;
LIST TimeOfDay = "день" "вечер" "утро" "ночь" ;
LIST Polovina = "половина" ;
LIST CardPoint = "западный" "южный" "юго-западный" "северный" "восточный" "юго-восточный" "северо-восточный" "северо-западный" "центральный" ;
LIST TimePart = "начало" "конец" "течение" "середина" "половина" ;
LIST LetterEnum = "модель" "план" "вариант" "коэффициент" "витамин" "марка" "полимиксин" "система" ;
LIST PersonTitleMsc = "князь" "император" "принц" "атаман" "царь"  "митрополит" ;
LIST PersonTitleFem = "императрица" ;

LIST PersonTitleBoth = "президент" "генерал" "секретарь" "обер-прокурор" ;
LIST Profession = "историк" "академик" "химик" "секретарь" "философ" "врач" ;
LIST Toponymy = "река" ;

SET PersonTitle = PersonTitleMsc | PersonTitleFem | PersonTitleBoth ;

LIST Letters = ("же" n) ("у²" n) ("о²" n) ("и²" n) ("а²" n) ("в" n) ("с" n) ("к" n) ; # etc.

LIST Notes = ("до" n) ; # etc.

LIST InsAdv = "часом" "потом" "кругом" "рядом" "летом" "разом" "весной" "зимой" "осенью" "утром" "вечером" "днём" "ночью"  ;

LIST AccAdv = "вечер" "день" "раз" "минута" "неделя" "год" ;

LIST PurposeAdv = "нечаянно" "назло" "нарочно" "напоказ" "спьяну" "спроста" "специально" "наперекор" "зря" "невзначай" "умышленно" "насмех" "преднамеренно" "ненамеренно" "бесцельно" "неспроста" "ненароком" "случайно" ;
#adverbs of purpose

LIST MeasureDegreeAdv = "абсолютно" "совсем" "очень" "страшно" "крайне" "соврешенно" "чересчур" "почти" "чрезвычайно" "достаточно" "слегка" "слишком" "исключительно" "гораздо" "много" "немного" "мало" "немало" "абсолютно" "чуть" "чуть-чуть" "еле" "еле-еле" "столь" "весьма" "почти" "никак" "едва" "капельку" "настолько" "так" "неприлично" "невероятно" "более" "наиболее" "глубоко" "мертвецки" "ничтожно" "смехотворно" "по-черному" "гораздо" "намного" "слегка" "немного" "сильно" "менее" "просто" "вечно" "донельзя" "насыщенно" "тяжело" "не по-детски" "очаровательно" "потрясающе" "возвышенно" "страшно" "по-доброму" "по-прежнему" "по-рыцарски" "ничють" "отнюдь" "вовсе" "хорошо" "весьма" "нелепо" "баснословно" "излишне" "чересчур" "возмутительно" "вызывающе" "скандально" "на удивление" "по уши" "по-весеннему" "чуть-чуть" "по-дружески" "реально" ;
#adverbs of measure and degree

LIST TempLocAdv = "вовне" "донельзя" "доныне" "поныне" "заранее" "зазря" "извне" "навсегда" "назавтра" "повсюду" "позавчера" "послезавтра" "отсюда"  "откуда" "отныне" "повсюду" "завтра" "сегодня" "вмиг" "сразу" "надолго" "долго" "немного" "часто" "редко" "неоднократно" "ежедневно" "помесячно" "дважды" "трижды" "многажды" "теперь" "сейчас" "раньше" "поздно" "рано" "послезавтра" "сегодня" "вмиг" "сразу" "уже" "часом" "потом" "кругом" "рядом" "летом" "разом" "весной" "зимой" "осенью" "утром" "вечером" "днём" "ночью";
#These adverbs include adverbs of duration and multiplicity (taken from rusgram.ru) + adverbs from LIST InsAdv

LIST Title = ("<[А-ЯA-Z][а-яa-z]+>"r) ;
LIST Majusc = ("<[А-ЯA-Z]+>"r) ;
LIST Minusc = ("<[а-яa-z]+>"r) ;

LIST DicTitle = ("[А-ЯA-Z][а-яa-z]+"r) ;
LIST DicMajusc = ("[А-ЯA-Z]+"r) ;
LIST DicMinusc = ("[а-яa-z]+"r) ;

LIST Prob = prb ;

LIST Facul = fac ;

LIST V/Gen = "достичь" "достигнуть" "добиться" "бояться" "выдерживать" "выражать" "давать" "добавлять" "добиваться" "добывать" "дожидаться" "допускать" "жаждать" "жалеть" "желать" "заменять" "засыпать" "избегать" "искать" "касаться" "колоть" "кушать" "лишать" "лишаться" "набирать" "наливать" "ожидать" "отдыхать" "покупать" "предвидеть" "прибавлять" "приносить" "просить" "проходить" "рекомендовать" "слушаться" "спрашивать" "стесняться" "стоит" "требовать" "тянуть" "удерживать" "хватит" "хоронить" "отсутствовать" "отговорить" "опасаться" "вспоминать" "хотеть" "требовать" ;

LIST V/Dat = "адресовать" "варить" "вводить" "вернуть" "взглянуть" "возить" "возмещать" "возражать" "вручать" "встречаться" "выписать" "гарантировать" "готовить" "грозить" "гулять" "дарить" "докладывать" "жаловаться" "жать" "изменять" "кивать" "класть" "копать" "кричать" "курить" "ломать" "махать" "менять" "мешать" "мыть" "навязывать" "надоедать" "назначать" "напоминать" "направлять" "ненавидеть" "обучать" "оказывать" "описывать" "определять" "осложнять" "оставаться" "открывать" "отозвать" "отражать" "отсылать" "перевязывать" "повторять" "подтверждать" "подчинять" "показывать" "помогать" "посвящать" "предоставлять" "предпочитать" "принадлежать" "присваивать" "радоваться" "раскрывать" "резать" "рекомендовать" "ремонтировать" "ручаться" "светить" "служить" "собирать" "советовать" "сообщать" "соответствовать" "сопровождать" "составлять" "таскать" "удвоить" "удивляться" "упрощать" "устраивать" "характеризовать" "чинить" "явиться" "нравиться" "предать" "учить" "противопоставлять" ; # Clean up this list! # Check "предать"/"учить"!

LIST N/Ptcp = "арестованный" "ведущий" "верующий" "вошедший" "входивший" "курящий" "заведующий"  "заключенный" "отдыхающий" "погибший"  "подозреваемый" "потерпевший" "приезжающий" "прооперированный" "раненный" "случившееся" "убитый" "управляющий" "каждый поздравляющий" "каждый приезжающий" "каждый входивший" "каждый поздравляемый" "каждый назначенный" "каждый награжденный" ;

LIST N/Ptcp-Neut = "будущее" "минувшее" "сказанное" "всё случившееся" "всё разбившееся" "всё светящееся" "всё существующее"  "всё летающее" "всё ползающее" "всё придуманное"  "всё принесённое" "всё завёрнутое" ;

LIST V/Ref = ("[а-яa-z]+ся"r) ;
LIST V/PstPssPerf = ("[а-яa-z]+(нный|тый)"r) ;
LIST V/PstActPerf = ("[а-яa-z]+(в)?ший"r) ;
LIST V/PstActPerf-Nu = ("[а-яa-z]+нувший"r) ;
LIST V/PssPrs = ("[а-яa-z]+(ый|мый)"r) ;

# Sets
# ====

SET WORD = N | V | A | Pr | Pron | Det | Adv | CC | CS | Interj | Num | Abbr | Part | ("\?") ;


# Verb sets
# ---------
SET NOT-VERB = WORD - V ;

# Boundary sets
# ---------
SET S-BOUNDARY = CS | Interr | (";") | BOS | EOS ;

# Case sets

# Some case, but not...
SET NOTNOMPREP = Dat | Gen | Acc | Ins ;
SET SOMECASE = Nom | Acc | Gen | Dat | Ins | Prp ;
SET OBLIQUECASE =  Acc | Gen | Dat | Ins | Prp ;
SET NOTACC =  Nom | Gen | Dat | Ins | Prp | Loc ;

# Anything but the following case...
SET NOACC = WORD - Acc ;
SET NODAT = WORD - Dat ;
SET NOINS = WORD - Ins ;
SET NOACCDAT = WORD - Acc - Dat ;

SET Sem/Time = Month | Months | Year | Century | Season | Seasons | TimeOfDay | ("период") ;

SET Sem/Distance = ("километр") | ("метр") ;

SET Sem/Quantity = ("дюжина") ;

SET Sem/Weight = ("килограмм") | ("грамм") ;

SET Sem/Unit = Sem/Time | Sem/Distance | Sem/Weight | Sem/Quantity ;

SET Sem/Person = PersonTitle | Profession ;

SET MscFemNeu = Msc | Fem | Neu ;

SET Prep/Prp = Pr/Pri | Pr/Na | Pr/V | Pr/O | Pr/Po ;

SET Prep/Ins = Pr/S | Pr/Nad | Pr/Pod | Pr/Meždu | Pr/Pered ;

SET Prep/Acc = Pr/Na | Pr/V | Pr/Po | Pr/Pod | Pr/Čerez ; # complete

SET Prep/Dat =  Pr/K | Pr/Soglasno | Pr/Po | Pr/Blagodarja ;

SET Prep/Gen = Pr/Protiv | Pr/Naprotiv | Pr/Vnutri ; # complete

SET NBR = Sg | Pl ;

SET MFN = Msc | Fem | Neu ;

SET NGDAIP = Nom | Gen | Dat | Acc | Ins | Prp | Loc ;

SET NGDAIPGenDatAccIns = Gen | Dat | Acc | Ins ;

SET NGDAIP/NBR = NGDAIP + NBR ;

SET Modif = Det | A - Short | Ord | ("же" part) ; # Fix tag of že

SET ModAcc = Modif + Acc ;
SET ModPrp = Modif + Prp ;

SET SentNotExcl = (".") | ("...") | ("?") | (":") | ("-")  ;

SET Punct = Guio | Cm | Excl | Ques | Comm | Dash | Col | SemCol ;

SET CLB = Rel | CS ;

SET Ptcp = PrsAct | PstAct | PrsPss | PstPss ;

SET Ptcp-noPass = PrsAct | PstAct ;

SET MOD =  A | Pron | Num | Det | Ptcp ;
#pos modifying a noun

SET ModLoc = MOD + Loc ;

###############################################################################
# Rules that should be fairly safe.
###############################################################################

SECTION

SELECT Pr IF (0 Oneletterp);

SELECT CC IF (0 I);

REMOVE:AdvNotPr Pr IF (0 Adv) (NOT 1 N + OBLIQUECASE OR A + OBLIQUECASE OR Pron + OBLIQUECASE OR Num);

## Non-sentences

SELECT Nom IF (-1 BOS) (0C N) (1 EOS) ;

## Safe operations at sentence boundaries

REMOVE Prp IF (-1 BOS) ;

REMOVE Pr IF (1 EOS) ;

REMOVE Det IF (1 EOS) ;
  #!  Холодная война -- противостояние между СССР и странами соцлагеря с одной стороны, и США и странами Западной Европой -- с другой.

## Safe lexicalised operations

SELECT Adv IF (0 I) (-1 No) ;

SELECT $$NGDAIP IF (-1C A + $$NGDAIP)(0 N);

## Safe operations around punctuation

REMOVE Pr IF (1C Cm) ;

SELECT Posle IF (-1C Cm) ;

REMOVE Interj IF (1 SentNotExcl) ;
  # Мне случилось говорить с одним из наших писателей о комизме в жизни, о трудности определить явление, назвать его настоящим словом.

## Safe operations with preposition/adverb ambiguities

REMOVE Pr IF (0C Pr OR Adv) (1C Nom) ;

REMOVE Pr IF (1C Fin) ;

REMOVE Pr IF (-1C Pr) ;

REMOVE Pr IF (1C Pr) ;

## Safe operations with short-form adjectives

REMOVE A + Short IF (1C Fin OR Adv OR A) (0C Short OR Adv) ;

REMOVE A + Short IF (-1C Fin OR Adv OR A) (0C Short OR Adv) ;

## Hopefully safe operations with несколько

SELECT Gen IF (-1 Num/Neskolko) (1C* N + Gen BARRIER (*) - Gen) (0 A + Gen) ;

## Safe operations after prepositions

REMOVE Adv/Neskolko IF (-1C Pr) (1 Gen) ;

REMOVE Nom IF (-1C Pr) ;

#моей as prn, if "из моей школы"
REMOVE A IF (-1C Pr)(0 Pron + Poss)(0 A);

REMOVE NGDAIP - Gen IF (-1C* Pr/Dlja OR Pr/U OR Pr/Do OR Pr/Ot OR Pr/Iz OR Pr/Iz-za OR Pr/Krome OR Pr/Protiv OR Pr/Posle OR Pr/Bez OR Pr/Sredi OR Pr/Okolo OR Pr/Pomimo CBARRIER (*) - Adv - Comp - DetIndecl) ;

REMOVE NGDAIP - Gen - Ins - Acc IF (-1C* Pr/S CBARRIER (*) - Adv - Comp - DetIndecl) ;

REMOVE Anim IF (0C Ord) (0C Cipher) (-1 Pr) (NOT 1 Anim) ;

#REMOVE NGDAIP - Acc - Prp - Loc IF (-1C* Pr/V OR Pr/Na CBARRIER (*) - Adv - Comp - DetIndecl - ModAcc - ModPrp) ;
REMOVE NGDAIP - Acc - Prp - Loc IF (-1C* Pr/V OR Pr/Na BARRIER (*) - Adv - Comp - DetIndecl - ModAcc - ModPrp) ;
   #! В 1960-х электрифицированные высокоскоростные железные дороги появились ...

REMOVE NGDAIP - Acc - Dat - Prp IF (-1C* Pr/Po CBARRIER (*) - Adv - Comp - DetIndecl) ;

REMOVE NGDAIP - Acc - Ins IF (-1C* Pr/Za CBARRIER (*) - Adv - Comp - DetIndecl) ;
  #! что у неё за чары

REMOVE NGDAIP - Gen - Ins IF (-1C* Pr/Meždu CBARRIER (*) - Adv - Comp - DetIndecl) ;

REMOVE NGDAIP - Dat IF (-1C* Pr/K OR Pr/Blagodarja OR Pr/Soglasno CBARRIER (*) - Adv - Comp - DetIndecl) ;

REMOVE NGDAIP - Prp IF (-1C* Pr/O CBARRIER (*) - Adv - Comp - DetIndecl) ;

REMOVE NGDAIP - Ins IF (-1C* Pr/Nad CBARRIER (*) - Adv - Comp - DetIndecl) ;

REMOVE NGDAIP - Acc IF (-1C* Pr/Čerez OR Pr/Vključaja CBARRIER (*) - Adv - Comp - DetIndecl) ;

SELECT A + $$NGDAIP IF (0C A) (-1C A + $$NGDAIP LINK -1C* Pr BARRIER (*) - A - Adv - Det) ;

REMOVE Pers IF (0C Pers OR Det) (NOT 0 Pprep) (-1C Pr) ;
  # Так что для меня вся моя университетская наука как бы для того только и существовала под конец, чтобы ...

REMOVE Loc IF (NOT -1* Prep/Prp) ;

SELECT Pr IF (0 Prep/Prp) (1C* Prp BARRIER (*) - Prp - DetIndecl) ;

## Safe operations with dates

SELECT Gen IF (0 Year) (-1 Num LINK -1 Months LINK -1 Pr/V) ;

SELECT Gen IF (0 Year) (-1 Num LINK -1 Months + Gen LINK -1 Num) ;

SELECT Dat IF (0 Year) (-1 Num LINK -1 Pr/K) ;

REMOVE NGDAIP - Gen - Loc IF (0 Year) (-1 Num LINK -1 Pr/V) ;

SELECT Neu IF (0 Dat) (-1 Year) (1 Nazad) ;

## Safe operations in some numeral expressions

SELECT Gen IF (-1 Percent) ;

SELECT Gen + Pl IF (-1 Sto OR Tysjača) ;

SELECT Gen + Pl IF (0C N OR A) (-1* Pjat BARRIER (*) - Adv - Gen);

## Safe operations with prepositional case

REMOVE Prp IF (-1C Inf)  ;

REMOVE Prp IF (NOT -1* Pr BARRIER CLB)  ;

REMOVE Prp IF (-1C N) (NOT 0 Prop LINK -1 Sem/Person) ;

## Safe operations with determiners

REMOVE Det IF (0 Det OR Pron) (1C Fin OR CS OR Pr) ;

REMOVE Det IF (0 Det OR Pron) (1C Ne) ;

REMOVE Det IF (0 Det OR Pron) (1 Cm LINK 1 CC OR CS) ;

#REMOVE Det IF (0 Det OR Pron) (1C* Fin BARRIER (*) - Adv) ;
REMOVE Det IF (0 Det OR Pron) (1C* V BARRIER (*) - Adv) ;

## Safe options with proper names

SELECT Ant + $$NGDAIP IF (1C Cog + $$NGDAIP) ;

SELECT Cog + $$NGDAIP IF (-1C Ant + $$NGDAIP) ;

## Safe operations with L2 Error tags

SELECT Err/L2_ii IF (0C Err/L2_ii OR Err/L2_revIkn);

###############################################################################
# Heuristic rules that should be fairly safe.
###############################################################################

SECTION

## Heuristic operations on single-character words

SELECT Pr (0 O) (1 Prp) ;

SELECT Pron IF (0 Ja) (-1 BOS OR CS) ;

SELECT Pron + Nom IF (0 Ja) (1* Fin + Sg1 BARRIER Fin) ;

## Heuristic operations

SELECT CS IF (0 Čto) (-1 Cm LINK NOT -1 Prn/To) (NOT 1* Ques);

REMOVE Fem IF  (0C A) (0C Msc OR Fem) (1C N) (NOT 1 Fem);

REMOVE Msc IF (0C A) (0C Msc OR Neu) (1C N) (NOT 1 Msc);

REMOVE NGDAIP - Prp IF (0C A OR Det) (1C* N + Prp BARRIER NGDAIP - Prp);

REMOVE NGDAIP - Ins IF (0C A OR Det) (1C* N + Ins BARRIER NGDAIP - Ins);

REMOVE Short IF (-1C Pr) ;

REMOVE Acc IF (-1* BOS BARRIER NGDAIP - Nom) (0C Nom OR Acc) (NOT 1* TV) (NOT -1 Num LINK 1 Sem/Time OR Sem/Distance) (NOT 0 Sem/Time) (NOT 0 Sem/Distance) ;

REMOVE Acc IF (-1* BOS BARRIER Fin OR NGDAIP - Nom)  (0C Nom OR Acc) (NOT 1* TV) (NOT -1* Prep/Acc) (NOT 0 Sem/Time) (NOT 0 Sem/Distance) ;

REMOVE:PoNNgen Acc IF (-1 N + Dat LINK *-1 ("по") BARRIER NODAT) (0 N + Gen) ;
REMOVE:PoNNGen  Nom IF (-1 N + Dat LINK *-1 ("по") BARRIER NODAT) (0 N + Gen) ;
## пю принципу самоуправления.

REMOVE Cmp IF (0 Cmp OR Adv) (NOT 1 Guio) ; # (NOT 1 Guio LINK 1 A) ;

SELECT Cmp IF (0 Cmp OR Adv) (1 Guio LINK 1 A LINK 1 N) ;

SELECT Adv IF (0C InsAdv) ;

REMOVE Adv IF (0C N + Ins OR Adv) (NOT 0 InsAdv) ;

REMOVE Acc IF (-1C Pr/S) (NOT 0 Sem/Unit) ;

# These need improvement
SELECT Letters IF (0 Majusc OR Title) (-1 Minusc) (NOT -1 BOS) ;

REMOVE Letters IF (NOT -1 Quot OR Parens OR Apos) (NOT 1 Quot OR Parens OR Apos) (NOT -1 N/Bukva) (NOT -1 Pr) (NOT -1 LetterEnum) (0 Minusc) ;

REMOVE Notes IF (NOT -1 Quot OR Parens OR Apos) (NOT 1 Quot OR Parens OR Apos) (NOT -1 N/Bukva) (NOT -1 Pr);

REMOVE Nom IF (-1C NumNotNom) ;
#! Для соседних с Руандой государств руандийские события апреля - июля 1994 года вылились в огромное число прибывших беженцев.

REMOVE Pl IF (0 N/Goda) (-1 LesserNum LINK NOT -1 Gen) ;

SELECT Msc IF (-1C Pr) (0 LesserNum) (1 N/Goda) ;

SELECT Gen IF (0 N/Goda) (-1 Num LINK -1 Seasons OR Months);

SELECT Gen IF (0 N/Goda) (-1 Num LINK -1 Seasons OR Months LINK -1 Pr);

SELECT Gen IF (0 N/Goda) (-1 Num LINK -1 Seasons OR Months LINK -1 Guio LINK -1 Seasons OR Months LINK -1 Pr);

SELECT Gen IF (0 N/Goda) (-1 Num LINK -1 Months LINK -1 Num);

SELECT Gen IF (0 N/Goda) (-1C Num) (1C EOS) ;

SELECT Gen IF (0 N/Goda) (-1 Month OR Seasons OR Day) ;

SELECT Gen IF (0 N/Goda) (-1 A LINK -1 Month OR Seasons OR Day) ;

SELECT Gen IF (0 N/Goda) (-1 A LINK -1 Num LINK -1 Pr) ;

SELECT Gen IF (0 N/Goda) (-1 Num LINK -1 Pr) ;

REMOVE Fem IF (0C Num) (1C N + Msc + Sg + Gen)  ;

REMOVE Neu IF (0C Num) (1C N + Msc + Sg + Gen)  ;

SELECT Gen IF (0 N/Goda) (-1C LesserNum) (1 Nazad) ;

SELECT Prp IF (-1C Pr) (0C Ord) (1C N/Godah) ;

SELECT Inf IF (-1 Fin) (0 Inf OR Imper) (1 EOS) ;

## Parts: половина, часть, протяжение, прошествие, течение, (начало, конец,)

#REMOVE Pl IF (0 N/Goda) (NOT -1* Num BARRIER NGDAIP - Nom OR Acc)

#SELECT N + $$NGDAIP IF (0 A OR N) (-1C A + $$NGDAIP LINK -1* Pr CBARRIER (*) - $$NGDAIP) (1C N) (NOT 1C N + $$NGDAIP) ;
  # По официальным данным правительства Руанды, число погибших составило 937 тысяч человек.
  #! Мать Кардуччи, умная и образованная женщина, противница всякого ханженства, внушила ему в ранние детские года горячий патриотизм и ненависть к немцам, и по трагедиям Альфьери и патриотическим одам Верни учила его азбуке.

REMOVE Pr/Nad IF (0 Pr/Nado) (NOT 1 PrefixNado) ;

REMOVE Pr/Nad IF (1C Inf) ;

SELECT N/Potom IF (-1 V/Oblivatsja OR V/Pahnut OR V/Isteč) ;

SELECT N/Potom IF (1 CC LINK 1 N/Krov) ;

REMOVE N/Potom IF (NOT -1 Prep/Ins OR A + Ins);

REMOVE N/Tomu IF (NOT -1* Pr/Po LINK -1* V/Vypuskat) ;

REMOVE Msc IF (0C N/Golova) (0C Msc OR Fem) ;

REMOVE A/Malo IF (1 Gen) ;

REMOVE A/Malo IF (-1 Sliškom) ;

REMOVE Imper IF (0 Našej) (NOT 1* Excl) ;

REMOVE Interj IF (0 Ij/Na) (NOT 1* Excl) (NOT 1 Prn/Tebe) (NOT 1 Cm) ;

SELECT Loc IF (0 N/Beregu) (-1* Pr/Na CBARRIER (*) - Prp) ;

REMOVE Fin IF (-1C Pr/K) (0C Fin OR Dat) ;

REMOVE Fin IF (-1C Pr/Na) (0C Fin OR Loc) ;

SELECT Ord IF (0 Num OR Ord) (1C N/Vek) ;

SELECT $$NGDAIP IF (0 Ord) (1 Guio) (2 Ord) (3 N/Vek + $$NGDAIP) ;

SELECT $$NGDAIP IF (-2 Ord) (-1 Guio) (0 Ord) (1 N/Vek + $$NGDAIP) ;

SELECT Paren IF (-1 Cm) (0 Paren OR Short) (1 Cm) ;

SELECT N/Mater IF (0 N/Mat OR N/Mater) (-1 A/Božij) ;

SELECT N/Mater IF (0 N/Mat OR N/Mater) (1 A/Božij) ;

SELECT Interr IF (0 Prn/Komu) (NOT -1* Interr BARRIER Interr) (NOT 1* Interr BARRIER Interr) (1* Ques) ;

SELECT Rel IF (0 Prn/Komu) (-1 Cm LINK -1 Prn/Tot) ;

SELECT N/Vek IF (0 N/Veko OR N/Vek) (-1 Roman LINK -1 TimePart)  ;

SELECT N/Vek IF (0 N/Veko OR N/Vek) (-1 Roman LINK -1 Pr)  ;

SELECT Gen IF (0 N/Vek) (0C Sg + Gen OR Pl) (-1 Roman LINK -1 TimePart) ;

SELECT Gen IF (0 N/Vek) (0C Sg + Gen OR Pl) (-1 Roman LINK -1 Guio LINK -1 Roman LINK -1 TimePart) ;

SELECT Gen IF (0 N/Vek) (0C Sg + Gen OR Pl) (-1 A + Gen LINK -1 TimePart) ;

SELECT Gen IF (-1 BOS)(0 Pron)(1 N) ;
## Его друг

SELECT Loc IF (-1 Pr/O) (0 N/Polku) ;

REMOVE Nom IF (0C Pl + Nom OR Sg + Gen) (1 EOS) (-1* Fin BARRIER Fin OR CS) (NOT -1* Fin + Pl BARRIER Fin OR CS);

SELECT Paren/Konečno IF (-1 Cm) (1 Cm) ;

REMOVE TV - Pass IF (NOT 1* Acc) (NOT -1* Acc) ;

REMOVE Gen IF (-1 BOS) (0 Title OR Majusc)(NOT 0 Pron) (NOT 1* V/Gen);

REMOVE Imper IF (1C Fin) ;

SELECT Prp IF (0C N) (-1* Pr/Na OR Pr/V CBARRIER (*) - Prp) (-1C Prp) (-1C Modif) ;

SELECT A + $$MFN IF (0C A + $$NGDAIP) (1C N + $$MFN + $$NGDAIP) ;

SELECT A + $$NGDAIP IF (0C A + $$MFN ) (1 N + $$MFN + $$NGDAIP) ;

REMOVE CC IF (0 Takže OR Tože) (-1 CC) ;

REMOVE CC IF (0 Takže OR Tože) (-1 Cm LINK -1 Rel) ;

REMOVE CC IF (0 Takže OR Tože) (-1C Fin) (NOT 1* Fin) ;

SELECT Kak + CC IF (1* Tak_I BARRIER Fin) ;

SELECT Gen + Sg IF (0 Ord) (1C N/Vek + Gen) (-1 TimePart LINK -1* Pr BARRIER (*) - Modif) ;

SELECT TV IF (-1C Nom) (1C Acc) (0C V) (0C IV OR TV) (NOT 1 AccAdv) ;

REMOVE Sam + Acc IF (-1 BOS) (1C* Nom BARRIER NGDAIP - Nom) ;

SELECT Det + $$MFN IF (1C* N + $$MFN + $$NGDAIP BARRIER NGDAIP - $$NGDAIP) (0C Det + $$NGDAIP) ;

SELECT Pr IF (0 Pr/Pered) (1C Ins) ;

REMOVE Acc IF (-1 BOS) (1* Guio BARRIER Fin LINK 1* EOS BARRIER Fin) ;

REMOVE Acc IF (-1C Acc LINK -1 BOS) (0 Acc OR Nom) (1C Guio) ;

REMOVE Fin IF (-1C Pr);

SELECT $$NGDAIP IF (0C $$NGDAIP OR Imper) (-1C $$NGDAIP LINK -1* Pr BARRIER (*) - $$NGDAIP) ;

SELECT Pron IF (0C Prp) (0 Pron OR Det) (NOT 0 Num) (NOT 1* Prp BARRIER (*) - Adv - DetIndecl) ;

SELECT N IF (0C A OR N) (-1C A) (1C Guio) ;

SELECT Pr/Soglasno IF (-1 BOS OR Cm) (1C Dat) ;

REMOVE Pl + Acc IF (0C A) (0 Acc OR Gen) (1 Gen) (1* Guio) (NOT 1* TV) (NOT -1* TV);

SELECT Nom IF (-1C A + Nom LINK -1* BOS BARRIER NGDAIP - Nom) (0C N) (1C Guio) ;

REMOVE Pred IF (0 Pr/Protiv) (1 Gen) ;

SELECT Dem IF (-1 Pr) (0 Dem) (1 Cm LINK 1 Čto) ;

SELECT Prn/To IF (0C Prn/To OR Prn/Tot) (1 Cm LINK 1 Čto) ;

SELECT Prn/Tot IF (0C Prn/To OR Prn/Tot) (1 Cm LINK 1 Kto) ;

SELECT V IF (-1C Ne) (0 Bylo) (1 Gen) ;

REMOVE Imper IF (-1C V LINK 0 Bylo) ;

SELECT Gen IF (-1C N LINK 0 NGDAIP - Gen) (0C A) (1C N + Gen) ;

REMOVE Short IF (0 Pr/Soglasno) (1 Dat) ;

SELECT Acc IF (-1 Pr/Na OR Pr/V) (0C Modif) (0C Acc OR Prp) (1C N + Acc);

REMOVE Fin IF (-1C Fin) ;
  ## На второй период приходится начало феодальной раздробленности на Руси.

REMOVE Fin IF (1C Fin) ;

REMOVE Imper IF (-1C Pr) (0 Imper OR N) (1C Gen);

REMOVE Acc IF (1 EOS) (NOT 0 AccAdv) (NOT -1* TV) (NOT -1* Prep/Acc) ;

#@ Лишь после того, как германская военная кампания против Польши завершилась и при этом @дело не дошло@ до перенесения военных действий на территорию Литвы, Сталин позволил себе распорядиться, чтобы Красная Армия при своем вступлении широким фронтом в Восточную Польшу (начиная с 17 сентября) времмено заняла стратегически важные районы Южной Литвы.

REMOVE Prp IF (-1C Fin) ;

REMOVE Det IF (0 Kotoryj) (1C Pron) ;

SELECT Pr IF (0 Pr/Posle) (1 Prn/Togo) (2 Cm) (3 Kak) ;

SELECT Pron + Neu IF (-1 Pr/Posle) (0 Prn/Togo) (1 Cm) (2 Kak) ;

REMOVE Acc IF (-1C SafeGuio) (NOT 0 Ord) (NOT -1* Acc BARRIER CLB) ;

REMOVE Det + $$MFN IF (1C N) (1C MFN - $$MFN) (0 MFN - $$MFN) (1 MFN - $$MFN) ;

SELECT Det IF (-1C Pr) (0 Det OR Pron) (0 Prp) (1C Prp) ;

REMOVE Acc IF (-1C Pr) (0 Modif) (0 Prp OR Acc) (1C Prp) ;

REMOVE Det + $$NGDAIP IF (1C N) (1C NGDAIP - $$NGDAIP) (0 NGDAIP - $$NGDAIP) (1 NGDAIP - $$NGDAIP) ;

#REMOVE CS/Čto IF (NOT -1* V/Čto) ;

REMOVE CS/Čto IF (-1 Pr) ;

SELECT Anim IF (0C Anim OR Inanim) (-1 SafeGuio LINK -1C Anim + Nom) ;

REMOVE Gen IF (0 Anim) (0C Gen OR Acc) (-1 TV) (-1C  Fin) (NOT -1 V/Gen) ;

SELECT V IF (0C Bylo) (1C PrcPast + Short);

SELECT Nom IF (0C Nom OR Acc) (-1C PrcPast + Short LINK -1C Bylo) (NOT -1* Nom) (NOT 1C* Nom) ;

SELECT $$MFN IF (0C Ord) (-1C Pr) (1C N + $$MFN) ;

# REMOVE Pred (-1C Fin) ;
  #! ... не было против.

REMOVE IV IF (0 TV OR IV) (1C Acc) (NOT 1 AccAdv) ;

REMOVE Acc IF (0C Nom OR Acc) (0 A) (-1 Short) (1C Nom) ;

REMOVE Adv IF (0 Pr/Posle) (-1C N) (1 Gen) ;

SELECT Pr IF (0 Pr/Pri) (0C* Fin BARRIER NGDAIP - Prp - Gen - Adv) ;

REMOVE Pr IF (0 Pr/Posle OR Adv/Posle) (NOT 1* Gen) (NOT 1 Unknown) ;

# Под эту статью туда уже не раз выделялись средства из российского бюджета, но после выяснялось, что они ушли на иные цели.
# не раз выделялись

SELECT Count IF (-1C Num) ;

REMOVE Acc IF (-1C Fin + IV) (NOT 0 AccAdv) ;

SELECT PrcPast IF (0C A OR PrcPast) (1C Pr) ;

REMOVE Nom IF (0 Nom OR Acc) (-1 V)(NOT -1 Ptcp)(NOT -1 A)(NOT -1 Jest) (-1 TV LINK -1C Nom) (0 A OR N) (NOT 0 AccAdv) ;
  #! Breaks with есть
  # works for давящая боль when adding (NOT -1 Ptcp)(NOT -1 A)

SELECT Acc IF (0C Acc OR Nom) (-1C Acc LINK -1* TV BARRIER N OR Pr OR CLB) (-1C* Nom);

SELECT Kotoryj + Nom IF (0 Kotoryj + $$NBR) (1C* IV + Fin + $$NBR BARRIER (*) - Adv) ;

#SELECT Acc IF (0C Acc OR Prp) (-1C Acc LINK -1* Pr/V OR Pr/Na BARRIER NGDAIP - Acc);
  #! На 26-е он успешно арестовал

REMOVE Ant IF (0C Prop) (-1C CardPoint OR Toponymy) ;

REMOVE Al IF (0C Prop + $$NGDAIP) (-1C Sem/Person + $$NGDAIP) ;

REMOVE Acc IF (0C N) (0C Acc OR Nom OR Gen) (-1C N + Acc) (NOT 0 Prop) (NOT -1 Sem/Person) (NOT 0 AccAdv) (NOT -1* Prep/Acc BARRIER NGDAIP - Acc) ;

REMOVE Acc IF (0C Acc OR Nom) (-1C A + Nom LINK -1 IV) (1 EOS) (NOT 0 AccAdv) ;

SELECT Gen IF (1C Prp OR Gen) (-1C Comp) ;

# REMOVE PrcPres IF (0 PrcPres OR A) (0 $$NGDAIP) (1C N) (1C $$NGDAIP) ;

SELECT CC IF (0 No) (-1 Cm) (1 Fin) ;

REMOVE CS IF (-1C Pr) ;

REMOVE Det IF (0 Det OR Pron) (1C Pred OR Short) (NOT 2 Guio OR A) ;

REMOVE Prp IF (-1C Ins) ;

SELECT Gen IF (-1C Modif) (-1C Gen + $$NBR) (0 Gen + $$NBR) (0C Acc OR Nom OR Gen) ;

SELECT A + $$NGDAIP IF (-1C Pr) (0C A OR N) (1C N + $$NGDAIP) ;

REMOVE Nom IF (-1C A + Acc) (0C N) (0C Acc OR Nom) ;
    ## Исполнительную власть осуществляли народные комиссары

REMOVE Acc IF (1C N + Nom) (0C A) (0C Acc OR Nom) ;

SELECT Pron IF (0 Prn/To) (1 Cm LINK 1 Čto) ;

SELECT Rel IF (0 Pron OR Adv) (-1 Cm LINK -1 Prn/To) ;

REMOVE V/Pass IF (NOT -1* Ins BARRIER Fin) (NOT 1* Ins BARRIER Fin) ;

SELECT Adv IF (-1 V/Byt) (1 PrcPast OR PrcPres) ;

SELECT Adv IF (0 Adv LINK 0 CC) (-1C Fin) (1 Modif LINK NOT 1* Fin BARRIER CLB);

SELECT Nom IF (-1 BOS) (0 Nom OR Acc) (1 Nom LINK 1 Short LINK 1 Guio) ;

SELECT N IF (0C N OR A) (-1 Pr) (1 EOS) ;

REMOVE Acc IF (NOT -1* TV LINK NOT 0 V/Pass) (NOT 0 AccAdv) (NOT 1* TV LINK NOT 0 V/Pass) (NOT -1* Pr BARRIER CLB) ;
  # barrier was (*) - Acc
  # works with "Надеюсь на следующий год." only after adding "год" to AccAdv

SELECT Nom IF (0C N) (0 Acc OR Nom) (-1 Modif) (-1* SafeGuio BARRIER (*) - Nom LINK -1C Nom) ;

SELECT $$NGDAIP IF (0C Roman) (1 N/Vek + $$NGDAIP) ;

SELECT $$NGDAIP IF (-1C A + $$MFN) (0C N + $$MFN) (1C Fin + $$MFN) ;

REMOVE Imper IF (-1C Pr) ;

REMOVE Prp IF (NOT -1* Prep/Prp BARRIER Fin) ;

SELECT Acc IF (0 N) (0 Nom OR Acc) (-1C A + Acc) (-1C* TV BARRIER NGDAIP - Acc LINK -1C* N + Nom BARRIER NGDAIP - Nom) ;

REMOVE CC IF (0 Že) (NOT -1* Fin OR Pred) ;

REMOVE CC IF (0 To) (-1 Pr) ;

SELECT Nom IF (0C N) (0C Nom OR Acc) (-1C A + Nom LINK -1 IV) ;

SELECT Nom IF (0 Kotoryj + $$NBR) (1 Fin + $$NBR LINK 1C Acc) ;

SELECT Nom IF (0C Nom OR Acc) (-1C* Nom BARRIER NGDAIP - Nom - Acc LINK -1C* IV BARRIER NGDAIP - Nom OR TV OR CLB OR Pr) ;

# SELECT Acc IF (0C A) (0C Nom OR Acc) (-1C N + Acc LINK -1C A + Acc) (1C N) ;
  #! В 1385 году великий князь литовский Ягайло Кревской унией обязался объединить в федеративное годударство Великое княжество Литовское и Королевство Польское в персональной унии в случав его избрания королем Польши.

REMOVE Acc IF (NOT 1* TV) (NOT 0 AccAdv) (NOT -1* TV) (-1C* IV BARRIER Fin) (NOT -1* Prep/Acc BARRIER NGDAIP - Acc) ;

SELECT Interr IF (0 Interr OR Rel) (-1 BOS) (1* Ques BARRIER CLB) ;
  # Где Ваня ?

SELECT Pr IF (0C Pr OR Adv) (0 Prep/Gen) (1 Gen) ;
  # Они всегда играют вместе, напротив большого дома.

REMOVE N + $$NGDAIP IF (0 A) (0 N) (1C N + $$NGDAIP) ;
# Ваня -- маленький мальчик шести лет.

SELECT Gen IF (0C Ant) (0 Gen OR Nom) (-1 Pr/U) ;
  # У Вани есть кошка.

###############################################################################
# Syntax labelling rules
###############################################################################

SECTION

LIST @ADVL = @ADVL ;
LIST @CVP = @CVP ;
LIST @CNP = @CNP ;
LIST @N→ = @N→ ;
LIST @P← = @P← ;

MAP @ADVL TARGET Short IF (NOT 0 V) (0C Short) (0C Neu) (1C Fin) ;

MAP @ADVL TARGET Short IF (NOT 0 V) (0C Short) (0C Neu) (-1C Fin) ;

#MAP @N→ TARGET Det IF (1C* N + $$NGDAIP BARRIER NGDAIP - $$NGDAIP) (0C Det + $$NGDAIP) ;

MAP @P← TARGET Pprep IF (-1 Pr) ;

# MAP @CVP TARGET Cm IF (-1* Fin CBARRIER Fin) (1* Fin CBARRIER Fin) (NOT 1* Cm CBARRIER Fin) ;
  #! Надвигается катастрофа, а министр, видимо, не понимает обстановки, и должные меры не принимаются.

MAP @CNP TARGET CC/I IF (0C CC/I) (NOT -1* Fin) (-1* N OR Modif) ;

MAP @CNP TARGET CC/I IF (0C CC/I) (NOT 1* Fin) (1* N OR Modif) ;

###############################################################################
# Really heuristic rules and hacks, relies on certain forms being extremely
# infrequent in relation to other forms.
###############################################################################

SECTION

REMOVE Dat IF (NOT 0 Prn/Sebe) (NOT 0 Anim OR Cog OR Ant) (NOT 0 Pron) (NOT 1* V/Dat) (NOT -1* V/Dat) (NOT -1* Prep/Dat) (NOT -1C A + Dat) ;
  # This rule is quite good, but should be after any rules that remove +Prp
  #! Мне не жарко
  #! Мне восем лет

SELECT Gen IF (0C N) (-1C A OR Det) (-1C Gen LINK -1 N LINK NOT 0 Gen) ;
  #! В конце прошлой [недели] собаки пропали [, а в начале этой -- нашлись]
  #! В начале наступающего собаки не лают.

REMOVE N/Denga ;

REMOVE Msc IF (0 N/Voda OR N/Vode) ;

SELECT Majusc IF (0C Roman) ;

SELECT DicMajusc IF (0C Roman) (0 Majusc) ;

SELECT DicMinusc IF (0C Roman) (0 Minusc) ;

REMOVE N/Pomoč IF (0 N/Pomoč OR V) ;

REMOVE V IF (0 V/Takaja) ;

REMOVE N IF (0 N/Vtora) ;

REMOVE V IF (0 Pr/Dlja) ;

REMOVE N IF (0 Pr/Pod) ;

REMOVE N/Mater IF (0 N/Mat OR N/Mater) ;

REMOVE Ij/Ih IF (NOT 1 Excl) ;

REMOVE Ij/I IF (NOT 1 Punct) ;

REMOVE CC/I IF (-1 CS) ;

REMOVE Adv/I IF (NOT -1* Adv/Ješë OR Jest OR Rel OR No BARRIER (*) - Adv) (NOT -1* CC BARRIER (*) - Adv) ;
  #! Существует и более мягкая точка зрения; её занимают ак отдельные соционики, критикующие, ...

REMOVE V/Primer IF (0 N/Primer) ;

REMOVE A/Metrov IF (0 N/Metr) ;

REMOVE V/Dela IF (0 N/Delo) ;

REMOVE A/Prišlij IF (0 V/Prijti) ;

REMOVE IV IF (0 V/Pisat) ;

REMOVE N/Pravilo² IF (0 N/Pravilo¹) ;

REMOVE Perf IF (0 V/Stroit) ;

REMOVE Perf IF (0 V/Vyhodit) ;

REMOVE Adv/Krugom² IF (NOT -1 V/Idjot LINK -1 N/Golova) ;

SELECT Adv IF (0 Adv/Celikom) ;

REMOVE N/Miro IF (NOT -1* N/Noga OR N/Lob) (NOT 1* N/Noga OR N/Lob) ;

REMOVE N/Byli IF (0 Cop) ;

REMOVE N/On IF (0C N OR Pron) ;

REMOVE N/Stradanje IF (0 N/Stradanie);

REMOVE Ij/A IF (NOT 1* Excl OR Ques);

REMOVE N/Slony IF (-1 A/Boevoj) ;

REMOVE N/Filma IF (0 N/Film OR N/Filma) ;

REMOVE Imper IF (0 Num/Tri) (NOT 1* Excl) ;

REMOVE Imper IF (0 N/Denj) (NOT 1* Excl) ;

REMOVE Interj IF (0 U) (NOT 1 Punct) (1 Gen) ;

REMOVE N IF (0 No) (NOT -1 Punct) (NOT 1 Punct) ;

REMOVE Ij/No IF (NOT 1* Excl OR Ques);

REMOVE Part IF (0 Bylo) (NOT -1* Past) (NOT 1* Past) ;

SELECT Pr IF (0C Vo) ;

SELECT DicTitle IF (NOT -1 BOS) (0 Title) (0C N OR EOS) ;

SELECT N/Čelovek IF (0C N/Čelovek OR N/Ljudi) ;

REMOVE Adv/Posle IF (0C Adv/Posle OR Pr/Posle) ;

REMOVE Prop IF (0 Pr/Po) (NOT 1 Prop) (NOT -1 Prop) (NOT 1 Fin) (NOT -1 Fin) (1* Dat OR Acc OR Ins) ;

SELECT Sg IF (0 Prop) (0 Gen) (-1 N) (1 EOS) (NOT -1* Fin + Pl) ;

SELECT Abbr IF (0 Abbr OR Interj) (-1C Num) (NOT 0 Pr) ;

SELECT Det IF (-1 Pr/Po) (0 Sej) (1 N/Denj) ;

REMOVE V IF (0 N/Maj) ;

SELECT Cog IF (0C Cog OR N) (-1 Ant) (0 Title) ;

SELECT Cog + $$MFN IF (-1C Ant + $$MFN) ;

SELECT Ant + $$MFN IF (1C Cog + $$MFN) ;

SELECT Msc IF (0C Ant) (-1C PersonTitleMsc) ;

SELECT Fem IF (0C Ant) (-1C PersonTitleFem) ;

SELECT Rel IF (0C Rel OR Interr) (-1 Cm) (NOT 1* Ques) ;

REMOVE Part IF (0 Kak) (NOT -1 Tak) ;

SELECT DicMajusc IF (-1 Abbr OR Sem/Person) (0 Abbr) (1 Cog) ;

SELECT Cog IF (1 Abbr LINK 1 Abbr) (-1 Sem/Person) ;

REMOVE Interr IF (0 Kak) (NOT 1* Ques) ;

REMOVE Part IF (0 Ili) ;

REMOVE Part IF (0 Adv/Ješë) ;

REMOVE N IF (0 No) ;

REMOVE Letters IF (NOT -1 Punct) (0C Letters) (NOT 1 Punct) ;

REMOVE Prop + Pl IF (-1C N) (0 Prop + Sg + Gen) (0C Prop) (0C Inanim) ;

REMOVE Part IF (0 Eto) (-1C Pr) ;

REMOVE Adv IF (0 Prop/Maša) ;

REMOVE Prob ;

REMOVE Facul ;

SELECT Pr IF (0 Pr LINK 0 Letters) (NOT -1 LetterEnum) ;

####!!!
SELECT N + $$NGDAIP IF (0C N) (-1 A + $$NGDAIP LINK -1 Samyj + $$NGDAIP) ;

SELECT Det IF (0 Samyj + $$NGDAIP) (1 A + $$NGDAIP LINK 1 N + $$NGDAIP) ;

###############################################################################
# Rules requiring correct syntactic function labelling (risky!)
###############################################################################

SELECT $$NGDAIP IF (0 A + $$NGDAIP) (1 @CNP LINK 1 A + $$NGDAIP) ;

SELECT $$NGDAIP IF (0 N + $$NGDAIP) (1 @CNP LINK 1 N + $$NGDAIP) ;

SELECT $$NGDAIP IF (0 Prop + $$NGDAIP) (1 @CNP LINK 1 Prop + $$NGDAIP) ;

SELECT $$NGDAIP IF (0 A + $$NGDAIP) (-1 @CNP LINK -1 A + $$NGDAIP) ;

SELECT $$NGDAIP IF (0 N + $$NGDAIP) (-1 @CNP LINK -1 N + $$NGDAIP) ;

SELECT $$NGDAIP IF (0 Prop + $$NGDAIP) (-1 @CNP LINK -1 Prop + $$NGDAIP) ;

SELECT $$NGDAIP IF (0C Roman) (1 @CNP LINK 1C Roman LINK 1 N/Vek + $$NGDAIP) ;

###############################################################################
# Rules dealing explicitly with subreadings
###############################################################################

REMOVE SUB:1 Sent IF (1 Sent) ;

REMOVE SUB:1 Sent IF (NOT 1 Majusc OR Title) ;

REMOVE SUB:1 Sent IF (1 Cog) ;

SECTION
#Section 2: rules for specific contexts
###############################################################################
# Test rules on ambiguous participles and adjectives (uliana)
###############################################################################
#LEVEL 1: morphosyntactic properties
###############################################################################
#Rules for specific contexts (SC)
###############################################################################
#REMOVE N. REMOVE A OR V to leave out N

REMOVE:PTCP-SpecC-N1 N IF (0 V)(0 N + $$NGDAIP)(-1 Comm LINK -1 N + $$NGDAIP) ;
#(rnc)Тут цвет какого-то нового общества, ещё не устоявшегося, ещё кипящего в водовороте отбора, ещё в этом водовороте не выработались окончательные правила, говорящие "да" и "нет", но уже вознесённого на гребень успеха. => говорящие

REMOVE:PTCP-SpecC-N2 N IF (0 V) (0 N + $$NGDAIP)(-1 PurposeAdv)(1 N + $$NGDAIP) ;

REMOVE:PTCP-SpecC-N3 N IF (0 V) (0 N + $$NGDAIP)(-1 Comm LINK -1 N + $$NGDAIP)(1 Num LINK 1 N) ;
#optional rule

REMOVE:PTCP-SpecC-N4 V IF (0 V) (0 N + $$NGDAIP)(-1 Comm LINK -1 N)(1 Pr) ;
#...написанные руководителями команд, участвующих в RoboCup.

REMOVE:PTCP-SpecC-N5 V IF (0 V + $$NGDAIP) (0 N + $$NGDAIP)(-1 Comm LINK -1 N)(1 Pr) ;

REMOVE:PTCP-SpecC-N6.1 V IF (0 V + Gen) (0C N + Gen)(0C A + Gen)(-1 N)(1 V) ;
REMOVE:PTCP-SpecC-N6.2 A IF (0C A + Gen) (0C N + Gen)(0C A + Gen)(-1 N)(1 V) ;
#(rnc)...подавляющее большинство погибших составили вооруженные боевики, открывшие огонь по израильским силам во время их...

REMOVE:PTCP-SpecC-N7.1 N IF (0 Ptcp + $$NGDAIP + $$NBR)(0 N + Gen)(-1 N)(1 Pr LINK 1 N LINK 1 N + $$NGDAIP + $$NBR) ;
#(rnc)Когда подходишь близко к стаду спящих на воде моржей, то видишь только несколько бурых бугров, выдающихся из воды и свободно уносимых течением. => спящих (overlap with the rule SCPtcpA6) 
 
REMOVE:PTCP-SpecC-N7.2 V IF (0 Ptcp + $$NGDAIP) (0 N + Gen)(-1 N)(*1 Pr BARRIER N + $$NGDAIP) ;

REMOVE:PTCP-SpecC-N7.3 A IF (0 A + Gen) (0 N + Gen)(-1 N)(1 Pr) ;
#интервью ведущих с героем выпуска

REMOVE:PTCP-SpecC-N8.1 V IF (0 V + Gen) (0 N + Gen)(-1 N OR Pr)(1 N + Gen) ;
REMOVE:PTCP-SpecC-N8.2 A IF (0 A + Gen) (0 N + Gen)(-1 N OR Pr)(1 N + Gen) ;
REMOVE:PTCP-SpecC-N8.3 V IF (0 V + Gen) (0 N + Gen)(-1 N OR Pr)(1 Quot LINK 1 N + Gen) ;
REMOVE:PTCP-SpecC-N8.4 A IF (0 A + Gen) (0 N + Gen)(-1 N OR Pr)(1 Quot LINK 1 N + Gen) ;
REMOVE:PTCP-SpecC-N8.5 V IF (0 V + Gen) (0 N + Gen)(-1 N OR Pr)(1 Quot) ;
REMOVE:PTCP-SpecC-N8.6 A IF (0 A + Gen) (0 N + Gen)(-1 N OR Pr)(1 Quot) ;
#интервью ведущих школы злословия
#интервью ведущих "школы злословия"
#у ведущих школы злословия
#у ведущих "школы злословия"

REMOVE:PTCP-SpecC-N9.1 A OR V IF (0 A + Dat) (0 N + Dat)(-1 Pron LINK -1 N LINK -1 V) ;
REMOVE:PTCP-SpecC-N9.2 A OR V IF (0 A + Dat) (0 N + Dat)(-1 N LINK -1 V) ;
REMOVE:PTCP-SpecC-N9.3 V IF (0 A + Dat) (0 N + Dat)(-1 Pron LINK -1 N LINK -1 V) ;
REMOVE:PTCP-SpecC-N9.4 V IF (0 A + Dat) (0 N + Dat)(-1 N LINK -1 V) ;
#Однако в начале июля на совещании по развитию нефтегазового комплекса страны председатель правительства Владимир Путин устроил разнос своим подчиненным за скачок цен на нефтепродукты и тем самым запустил антимонопольный маховик на полную катушку. => подчиненным

#SELECT:Test N IF (0 Ptcp + Gen) (0C N/Ptcp + Gen OR N/Ptcp-Neut  + Gen)(0 A + Gen)(-1 N)(1 Pr) ;
#В остальном «Школа злословия» представляла собой интервью ведущих с героем выпуска.
#NB: for now this rule does NOT work.

REMOVE:PTCP-SpecC-N10 N IF (0 V) (0 N + $$NGDAIP)((1 N + $$NGDAIP) OR (1 Pr LINK 1 N LINK 1 N + $$NGDAIP)) ;
#adjunct
#Variant
#Курящие в стороне люди.

SELECT:PTCP-SpecC-N11 N IF (0 V) (0 N)(1 S-BOUNDARY)((-1 Num)OR (-1 Num LINK -1 V)) ;

#########################################################################################
#REMOVE V:

REMOVE:Der V + Der IF (0 V + Der) ;
#the rule removes double reading (A and V) so that it does not interfere into the following rules, the rule should be temporary. 

REMOVE:PTCP-SpecC-V1.1 V IF (0 Ptcp)(0 A + $$NGDAIP)(NOT 0 Adv)(NOT 0 N)(1 N + $$NGDAIP LINK NOT 1 Pr LINK 1 N OR Pron) ;
#single use
# (rnc)Для умных и думающих людей хочу напомнить: подготовка квалифицированных учителей – русистов в Азербайджане началась с 30-х годов прошлого века. => квалифицированных ok
# (rnc)Старый почтенный штурман, всю жизнь свою честно тянувший лямку и пользовавшийся уважением и адмирала, и всех своих сослуживцев, понял, конечно, и этот намек на то, что он любит выпить, почувствовал и этот высокомерный взгляд, но ничего не ответил. => почтенный
# (open corp)Зато с каким упорством многие соотечественники нападают на желающих порадовать любимую девушку букетом цветов и романтическим вечером в кафе или любимого мужчину неожиданным подарком в День святого Валентина . => любимую
#(opencorp)Приблизительно в это время сведения о неизвестной, выдающей себя за великую княжну Анастасию, просачиваются в прессу и доходят до Копенгагена, где безвыездно проживает вдовствующая императрица Мария Федоровна. => вдовстующая

REMOVE:PTCP-SpecC-V1.2 V IF (0 A + $$NGDAIP)(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(1 N + $$NGDAIP LINK NOT 1 Pr LINK 1 A LINK 1 N OR Pron) ;
#single use
#Вскоре после этого волнующего события было заявлено о начавшейся разработке принципиально новой для российской действительности модели автомобиля с таинственным, но интригующим названием «проект С». => волнующего
#Хотя мало кто знает, что «проект С» начал разрабатываться еще при Владимире Каданникове (бывший президент — генеральный директор АвтоВАЗа), а тот самый макет «новой модели» с того самого автосалона, по словам старожилов завода, успел покрыться толстым слоем пыли в лабораториях НТЦ еще задолго до прихода менеджеров «Рособоронэкспорта».=> бывший

REMOVE:PTCP-SpecC-V2.1 V IF (0 A + $$NGDAIP)(0 Ptcp)(NOT 0 Adv)(NOT 0 N)((-1 MeasureDegreeAdv LINK -1 Comm LINK -1 N + $$NGDAIP OR Pron + $$NGDAIP) OR (-1 MeasureDegreeAdv LINK -1 Ne LINK -1 Comm LINK -1 N + $$NGDAIP OR Pron + $$NGDAIP))(1 CC LINK NOT 1 Pron + NOTNOMPREP OR N + NOTNOMPREP) ;
#adverbs of measure/degree
#single use
# (rnc)― Так вы коллекционируете насекомых? ― спросил философ, настолько удивленный и обиженный, насколько было это возможно при его характере. => удивленный
#very specific context, no hits in opencorpora

REMOVE:PTCP-SpecC-V2.2 V IF (0 A)(0 Ptcp)(NOT 0 Adv)(NOT 0 N)((-1 CC LINK -1 A or V LINK -1 MeasureDegreeAdv)OR(-1 CC LINK -1 A LINK -1 MeasureDegreeAdv LINK -1 Ne))(1 Comm) ;
#adverbs of measure/degree
#single use
#― Так вы коллекционируете насекомых? ― спросил философ, настолько удивленный и обиженный, насколько было это возможно при его характере. => и обиженный
#very specific context, no hits in opencorpora

REMOVE:PTCP-SpecC-V3 V IF (0 A + $$NGDAIP + $$NBR)(0 Ptcp + $$NGDAIP + $$NBR)(NOT 0 Adv)(NOT 0 N)((-1 MeasureDegreeAdv OR Det) OR (-1 Ne LINK -1 MeasureDegreeAdv OR Det) OR (-1 V))(1 Pron + $$NGDAIP + $$NBR OR N + $$NGDAIP  + $$NBR) ;
#single use
#Мировую известность художникам Татьяне Арзамасовой, Льву Евзовичу и Евгению Святскому, по начальным буквам фамилий которых и назван коллектив (позже к художникам присоединился выдающийся фотограф Владимир Фридкес), принес скандальный «Исламский проект», в котором 100-метровый оплот американской независимости – статуя Свободы – облачается в паранджу, католический храм Саграда Фамилия вонзает в небо исламские полумесяцы, в Нью-Йорке горят небоскребы, а на Красной площади сидят арабские боевики с «калашами» наперевес. => выдающийся
#Вскоре после этого волнующего события было заявлено о начавшейся разработке принципиально новой для российской действительности модели автомобиля с таинственным, но интригующим названием «проект С».=> волнующего
#Если бы он курил, то он непременно курил бы сейчас, как это обычно делают курящие люди в задумчивости.=>курящие
#Если говорить о строительстве, то в Риге есть замороженные стройки и стоят деревянные дома, от которых в Москве за пять минут не осталось бы щепки – а если бы это был музей, то сделали бы то же самое из двойного бетона, но в Риге есть реально действующий рынок земли с понятными ценами и кучей объявлений в газете «Реклама». => действующий
## ptcp/adj + ptcp/adj + head noun
#Основной активной жалобой у больных, находящихся в сознании, является сжимающая давящая боль за грудиной.=>давящая

#comparative/superlative degree SCPtcpV5
#comparative degree
REMOVE:PTCP-SpecC-V4.1 V IF (0 Ptcp)(0 A)(NOT 0 Adv)(NOT 0 N)((-1 Adv/Cmpar) OR (-1 Ne LINK -1 Adv/Cmpar))(NOT 1 N + NOTNOMPREP OR Pron + NOTNOMPREP) ;
#На репетициях видели гораздо более собранного и звонкоголосого певца.=>собранного
#« Я — киноглаз, я создаю человека , более совершенного, чем созданный Адам, я создаю тысячи разных людей по разным предварительным чертежам и схемам…=>совершенного
# (rnc) «― спрашивает Дмитриев, все более и более удивленный и испуганный словами солдата. => удивленный

REMOVE:PTCP-SpecC-V4.2 V IF (0 Ptcp)(0 A)(NOT 0 Adv)(NOT 0 N)((-1 Samyj) OR (-1 Ne LINK -1 Samyj))(NOT 1 N + NOTNOMPREP OR Pron + NOTNOMPREP) ;
#superlative degree
#Variation:
#На репетициях видели самого собранного и звонкоголосого певца.=>собранного
#« Я — киноглаз, я создаю человека, самого совершенного,=>совершенного

REMOVE:PTCP-SpecC-V5 V IF (0 A + $$NGDAIP)(0 Ptcp)(NOT 0 Adv)(NOT 0 N)((-1 N + $$NGDAIP OR Pron + $$NGDAIP) OR (-1 Ne LINK -1 N + $$NGDAIP))(NOT 1 Punct)(NOT 1 Pr LINK 1 Loc) ;
#А любимее всего – финал открытый. => открытый
#Пожилая пара перед посещением выставки «Берлинская стена» находилась под впечатлением от архитектуры бывшей пивоварни: => бывшей
#где помещение бывшей шелковой фабрики => бывшей
#При сохранении без изменений существующей социально-политической системы. => существующей
# (rnc) В его истории революции фигурирует царь, престолопреемник, общественные и партийно-политические группировки, но главное действующее лицо и «деятель» революции отсутствует. => действующее

REMOVE:PTCP-SpecC-V6 V IF (0 V)(0 A)(0 Ptcp)(NOT 0 Pred)(NOT 0 Adv)(NOT 0 N)((-1 CC) OR (-1 Ne LINK -1 CC))(1 S-BOUNDARY or (WORD S-BOUNDARY)) ;
#Все грязное и обтрёпанное. => обтрёпанное
#Методика наших вычислений — упрощенная и схематичная, но зато прозрачная и открытая. => открытая

#This set of rules applies to borderlines cases, these are non-predicative uses of ambiguous wordforms. These examples are artificial.

REMOVE:PTCP-SpecC-V7.1 V  IF (0 A + $$NGDAIP)(0 Ptcp + $$NGDAIP)(NOT 0 Adv)(NOT 0 N)((-1 MeasureDegreeAdv) OR (-1 MeasureDegreeAdv LINK -1 Ne))(1 Pr LINK 1 N + Loc OR Pron + Loc OR Det + Loc OR Ptcp + Loc LINK 1 N + $$NGDAIP) ;
#adv. modifier + ambg. ptcp + preposition phrase + head noun
#adv + pr + n/pron-Prp

#(up) Это был достаточно уверенный в себе человек. => уверенный
#(up) Это был не достаточно уверенный в себе человек. => уверенный
#(up) Это был не достаточно уверенный в происходящем человек. => уверенный
#(up) Это был достаточно уверенный в происходящем человек. => уверенный
#(up) Это был достаточно уверенный в этом человек. => уверенный
#(up) Это был не достаточно уверенный в этом человек. => уверенный
#NOT found in open corpora

REMOVE:PTCP-SpecC-V7.2 V  IF (0 A + $$NGDAIP)(0 Ptcp + $$NGDAIP)(NOT 0 Adv)(NOT 0 N)((-1 MeasureDegreeAdv)OR(-1 MeasureDegreeAdv LINK -1 Ne))(1 Pr LINK 1 Det + Loc LINK 1 N + Loc LINK 1 N + $$NGDAIP) ;
#adv. modifier + ambg. ptcp + preposition phrase + head noun
#Pr + Det + noun + head noun
#(up) Это был достаточно осведомленный в этом вопросе человек. => осведомленный
#(up) Это был не достаточно осведомленный в данном вопросе человек. => осведомленный
#NOT found in open corpora

REMOVE:PTCP-SpecC-V7.3 V  IF (0 A + $$NGDAIP)(0 Ptcp + $$NGDAIP)(NOT 0 Adv)(NOT 0 N)((-1 MeasureDegreeAdv)OR(-1 MeasureDegreeAdv LINK -1 Ne))(1 Pr LINK 1 A + Loc LINK 1 N + Loc LINK 1 N + $$NGDAIP) ;
#adv. modifier + ambg. ptcp + preposition phrase + head noun
#Pr + adj + noun + head noun
#(up) Это был достаточно осведомленный в привычном распорядке человек. => осведомленный
#(up) Это был не достаточно осведомленный в привычном распорядке человек. => осведомленный
#NOT found in open corpora

REMOVE:PTCP-SpecC-V7.4 V IF (0 A + $$NGDAIP)(0 Ptcp + $$NGDAIP)(NOT 0 Adv)(NOT 0 N)((-1 MeasureDegreeAdv) OR (-1 MeasureDegreeAdv LINK -1 Ne))(1 Pr LINK 1 ModLoc LINK 1 N + Loc LINK 1 N + $$NGDAIP) ;
#adv. modifier + ambg. ptcp + preposition phrase + head noun
#Pr + WORD + head noun
#more general cases
#Это был достаточно уверенный в пяти случаях человек. => уверенный
#Это был не достаточно уверенный в пяти случаях человек. => уверенный
#NOT found in open corpora

########################################################################################

REMOVE:PTCP-SpecC-V8 V IF (0 Ptcp + $$NGDAIP)(0 A + $$NGDAIP)(NOT 0 Adv)(NOT 0 N)((-1 CC LINK -1 A OR N) OR (-1 V))(1 N + $$NGDAIP - Ins) ;
#v + amb.ptcp + head noun ; preposition
#no complement
#Поэтому к гидроизоляционным материалам, используемым на подобных кровлях, предъявляются повышенные требования. => повышенные
#(open corp)Общая фонетика и избранные статьи => избранные
#(open corp)президент «Консоли» Владимир Константинов был доверенным лицом Виктора Януковича на президентских выборах => доверенным

REMOVE:PTCP-SpecC-V9 V IF (0 Ptcp + Gen)(0 A + Gen)(NOT 0 Adv)(NOT 0 N)((-1 Adv - TempLocAdv) OR (-1 Adv -  TempLocAdv LINK -1 Adv) OR (-1 CC LINK -1 A OR N) OR (-1 N) OR (-1 N LINK -1 Ne))(1 N + Gen) ;
#single use
#adv + amb.ptcp + noun-genetive
#adv + adv + amb.ptcp + noun-genetive
#adj/noun + conj + amb.ptcp + noun-genetive
#noun + amb.ptcp + noun-genetive
#ne + noun + amb.ptcp + noun-genetive

#(variant)Поэтому к гидроизоляционным материалам, используемым на подобных кровлях, предъявляется список повышенных требований. => повышенных
#(open corp) В начале текущего месяца АвтоВАЗ подписал договор о намерениях с банком «Сосьете Женераль Восток» (BSGV) о предоставлении кредита на выкуп у Renault лицензий на производство автомобилей на платформах пяти- и семиместных универсалов R90 и фургона F90. => текущего
#(open corp)Югославская агрессия, казалось бы, стала лучшим подтверждением данного мнения. => данного
#(open corp)Пожилая пара перед посещением выставки «Берлинская стена» находилась под впечатлением от архитектуры бывшей пивоварни: => бывшей (overlap with the rule SCPtcpV6.2)

REMOVE:PTCP-SpecC-V10 V IF (0 Ptcp) (0 A + $$NGDAIP)(NOT 0 Adv)(NOT 0 N)(-1 CC OR Comm LINK -1 A)(1 Comm OR CC) ;
#postposed adjective
#adj + conj/comm + ambig.ptcp
#(open corp) Поэтому, чтобы нами управляли, руководили, считали там, люди компетентные, знающие, думающие и не просто за зарплату, а за совесть работающие, думающие о людях.=> , знающие, думающие
# (rnc) Все господа офицеры сидели в кают-компании, подавленные, удрученные и взволнованные и «позором» корвета, и адмиральским «ураганом», и, главное, судьбой этого отчаянного «Сереженьки», который решился назвать адмирала, да еще на шканцах, «бешеной собакой». => удрученные, взволнованные
# (rnc) Народ немолодой, почтенный, в большинстве семейный, а приходится как бы играть в революцию. => почтенный

#enumeration
REMOVE:PTCP-SpecC-V11 V IF (0 Ptcp)(0 A + $$NGDAIP)(NOT 0 Adv)(NOT 0 N)(-1 CC OR Comm LINK -1 A)(1 Comm OR CC) ;
#postposed adjective
#(open corp)Огромные размеры дореволюционных фабричных залов, захватывающий вид на Кремль и Пречистенскую набережную, а также островное местоположение делают «Красный Октябрь» уникальным местом в Москве.
# (rnc) На ногах сапоги желтые, шнурованные, выше колен. => шнурованные (if added to the lexicon)

REMOVE:PTCP-SpecC-V12  V IF (0 Ptcp)(0 A + Pred)(NOT 0 Adv)(NOT 0 N)(-1 N + $$NGDAIP + $$NBR OR Pron + $$NGDAIP + $$NBR LINK -1 Comm)(1 Comm) ;
##predicative use of adj, this should precede rule SCPtcpA4 ; 
# (open corp) — Но задатки виртуоза, я уверена, даны вам природой. => уверена
# (open corp) Основная же масса, я уверена, будет рада любому сокращению количества уроков и даваемого на них материала. => уверена

REMOVE:PTCP-SpecC-V13 V IF (0 Ptcp + Ins)(0 A + Ins)(NOT 0 Adv)(NOT 0 N)(-1 Pron + Acc OR N + Acc LINK -1 V)(1 Punct) ;
# VP construction: v + noun/pron-Acc + ambg.ptcp-Ins + comm
# (open corp) К сожалению , приходится признать , что так же мало понимания и осознанности у многих даже из тех , кто считает себя верующими , в том , что же на самом деле празднуется в самые большие и светлые религиозные праздники , такие как Пасха и Рождество . => верующими

REMOVE:PTCP-SpecC-V14 V IF (0 A + $$NGDAIP + $$NBR)(0 Ptcp + $$NGDAIP + $$NBR)(NOT 0 Adv)(NOT 0 N)(-1 Punct OR CS OR CC OR Pr OR Ili)(1C N + $$NGDAIP + $$NBR) ;
#single use 
# (open corp) Зато с каким упорством многие соотечественники нападают на желающих порадовать любимую девушку букетом цветов и романтическим вечером в кафе или любимого мужчину неожиданным подарком в День святого Валентина. => любимого
#does NOT work

#REMOVE A

#PTCP: predicative use

REMOVE:PTCP-SpecC-A1.1 A IF (0 Ptcp + Pred)(0 A)(NOT 0 Adv)(NOT 0 N)(-1 Cop)(1 S-BOUNDARY) ;
#use of participles as copular verb objects, predicative use of participles
#(up) Машина была угнана. => угнана

REMOVE:PTCP-SpecC-A1.2 A IF (0 Ptcp + Pred)(0 A)(NOT 0 Adv)(NOT 0 N)(-1 CC OR Comm LINK -1 Ptcp OR A LINK -1 Cop) ;
#use of participles as copular verb objects, predicative use of participles
#(up) Машина была угнана и продана. => продана

#REMOVE:PTCP-SpecC-A1.3 A IF (0 Ptcp + Pred)(0 A)(NOT 0 Adv)(NOT 0 N)(-1 Cop)(1 Pr) ;
#use of participles as copular verb objects, predicative use of participles
#(up) Кот был загнан в угол. => загнан

REMOVE:PTCP-SpecC-A1.4 A IF (0 Ptcp + Pred)(0 A)(NOT 0 Adv)(NOT 0 N)(-1 CC LINK -1 Cop)(1 Comm LINK 1 CC) ;
#use of participles as copular verb objects, predicative use of participles
#(up) Машина была не только найдена, но и возвращена владельцу. => найдена

REMOVE:PTCP-SpecC-A1.5 A IF (0 Ptcp + Pred)(0 A)(NOT 0 Adv)(NOT 0 N)(-1 CC lINK -1 Comm LINK -1 Ptcp + Pred) ;
#use of participles as copular verb objects, predicative use of participles
#(up) Машина была не только найдена, но и возвращена владельцу. => возвращена

REMOVE:PTCP-SpecC-A1.6 A IF (0 Ptcp + Pred)(0 A)(NOT 0 Adv)(NOT 0 N)(-1 CC/A LINK -1 Comm LINK -1 Ptcp + Pred LINK -1 Ne LINK -1 Cop) ;
#use of participles as copular verb objects, predicative use of participles
#(up) Двери были не открыты, а закрыты. => закрыты

REMOVE:PTCP-SpecC-A1.7 A IF (0 Ptcp)(0 A + $$NGDAIP)(NOT 0 Adv)(NOT 0 N) ((-1 Ne LINK -1 Comm LINK -1 N + $$NGDAIP OR Pron + $$NGDAIP) OR (-1 Comm LINK -1 N + $$NGDAIP))((1 N + NOTNOMPREP OR Pron  + NOTNOMPREP) OR (1 CC LINK 1 Ne LINK 1 Adv LINK 1 Pr LINK 1 N OR Pron)OR (1 Pr LINK 1 N OR Pron)) ;
#PTCP: adjuncts, complements
# (open corp) Но на деле для работающей схемы, включающей в себя и комфорт пользователей, нуждающихся в авторитетах и выстраивании личных связей, и доходы владельцев ресурса, имеющих собственное мнение, каким он должен быть, давно уже пора привлекать не программиста, способного поставить бесплатный движок форума phpBB, а как минимум социолога. => нуждающихся
# (open corp) Поведение людей, окружающих госпожу Чайковскую, показалось мне довольно подозрительным. => окружающих
# (rnc) Ястржембский, паркетный царедворец, не знающий свиста пуль, сделавший карьеру на том, что выносил за Ельциным ночную вазу, учит нас правильно понимать. => знающий
# (rnc) Когда подходишь близко к стаду спящих на воде моржей, то видишь только несколько бурых бугров, выдающихся из воды и свободно уносимых течением. => выдающихся 
# (rnc) Да и интеллектуальные тоже, люди, правящие миром, неспроста добились своего положения. => правящие 

########################################postposition####################################
REMOVE:PTCP-SpecC-A2 A IF (0 Ptcp + $$NGDAIP + $$NBR)(0 A + $$NGDAIP + $$NBR)(NOT 0 Adv)(NOT 0 N)((-1 Ne LINK -1 Comm LINK *-1 N + $$NGDAIP + $$NBR OR Pron + $$NGDAIP + $$NBR)OR (-1 Comm LINK *-1 N + $$NGDAIP + $$NBR))((1 N + NOTNOMPREP OR Pron  + NOTNOMPREP) OR (1 CC LINK 1 Ne LINK 1 Adv LINK 1 Pr LINK 1 N OR Pron)OR (1 Pr LINK 1 N OR Pron)) ;
#complement
#noun/pronoun + (in the same case with ambg ptcp) + comm + ne + ambg.ptcp noun/pronoun
# (open corp var) Кроме того, в ближайшее время будут подготовлены поправки в Бюджетный кодекс, не разрешающие регионам предоставлять ...

# noun + comm + ambg ptcp + noun (Dat, Gen, Acc, Ins)
# (open corp) Кроме того, в ближайшее время будут подготовлены поправки в Бюджетный кодекс, разрешающие регионам предоставлять гарантии по банковским кредитам в размере 50—70%, рассказал вице-премьер Алексей Кудрин. => разрешающие

# (rnc) Кстати, недавно многие задышали посвободнее — ВАЗ оплатил долги за поставки, совершенные в прошлом (!) году. => совершенные
# (rnc) Поэтому, чтобы нами управляли, руководили, считали там, люди компетентные, знающие, думающие и не просто за зарплату, а за совесть работающие, думающие о людях. => думающие о людях
# (rnc) Кстати, недавно многие задышали посвободнее — ВАЗ оплатил долги за поставки, совершенные в прошлом (!) году. => совершенные

REMOVE:PTCP-SpecC-A3  A IF (0 Ptcp + $$NGDAIP + $$NBR)(0 A + $$NGDAIP + $$NBR)(NOT 0 Adv)(NOT 0 N)((-1 N + Acc LINK -1 Adv LINK -1 Comm LINK -1 N  + $$NGDAIP + $$NBR)OR (-1 Pron + Acc LINK -1 Adv LINK -1 Comm LINK -1 N  + $$NGDAIP + $$NBR)OR (-1 Det + Acc LINK -1 Adv LINK -1 Comm LINK -1 N  + $$NGDAIP + $$NBR))(1 Comm) ;
#postposition
#adv(incl. measure/degree) + v.dependent-Acc + ambg.ptcp
#(rnc) Отсюда его знаменитые бегства — то, что так поражало людей, мало его знающих, и нравилось женщинам. => знающих

REMOVE:PTCP-SpecC-A4 A IF (0 Ptcp)(0 A)(NOT 0 Adv)(NOT 0 N)((-1 N LINK -1 Pr) OR (-1 N LINK -1 Det OR A LINK -1 Pr) OR (-1 N LINK -1 Pr) OR (-1 Adv))(1 Pr LINK 1 N) ;
#postposition
#preposit.phrase + ambg.ptcp + prep.phrase (prep + noun)
#adjunct
# (rnc)Образованный в суровую военную пору, ваш институт был с самого начала ориентирован на решение самых важных, самых сложных проблем отечественной газовой индустрии. => ориентирован
# (rnc)Когда подходишь близко к стаду спящих на воде моржей, то видишь только несколько бурых бугров, выдающихся из воды и свободно уносимых течением. => спящих (overlap with the rule SCPtcpN8.1)
# (rnc)Пройдя первый двор и оказавшись в точно таком же втором, Ирка высунула нос из-за своего невероятного букета и, заприметив мирно курящих в стороне мальчишек, подгребла к ним. => курящих 
#successful rule (3 true positives)

REMOVE:PTCP-SpecC-A5 A IF (0 Ptcp + $$NGDAIP + $$NBR)(0 A + $$NGDAIP + $$NBR)(NOT 0 Adv)(NOT 0 N)(-1 Adv LINK -1 Comm LINK -1 N + $$NGDAIP + $$NBR OR Pron + $$NGDAIP + $$NBR)(1 CC LINK 1 N + NOTNOMPREP OR Pron + NOTNOMPREP) ;
# postposition, adv of measure/degree, dependent-Ins
# (rnc) Тогда я готов пригласить вас назавтра на breakfast, ― ответил он, весьма удивленный не то нашей осведомленностью, не то нахальством, не то с ног сшибающей непосредственностью. => удивленный (NOT resolved)

REMOVE:PTCP-SpecC-A6 A IF (0 Ptcp)(0 A)(NOT 0 Adv)(NOT 0 N)((-1 MeasureDegreeAdv) OR (-1 MeasureDegreeAdv LINK -1 Adv) OR (-1 Ne LINK -1 MeasureDegreeAdv) OR (-1 Ne LINK -1 MeasureDegreeAdv LINK -1 Adv))(1 Pron + Ins OR Pron + Poss + Ins OR N + Ins OR A + Ins OR Det + Ins) ;
#compatibility with adverbs of measure and degree
# (rnc) Как правило, функционал подобного рода сайтов настолько продуман его разработчиками, что на интуитивном уровне понятен даже самым неискушенным в пользовании компьютером и Интернетом посетителям. => продуман 

REMOVE:PTCP-SpecC-A7 A IF (0 Ptcp)(0 A)(NOT 0 Adv)(NOT 0 N)((-1 CC) OR (-1 Ne LINK -1 CC) OR (-1 MeasureDegreeAdv) OR (-1 MeasureDegreeAdv LINK -1 Adv) OR (-1 Ne LINK -1 MeasureDegreeAdv) OR (-1 Ne LINK -1 MeasureDegreeAdv LINK -1 Adv) )(1 CC LINK 1 A + NOTNOMPREP OR Pron + NOTNOMPREP OR Det + NOTNOMPREP LINK 1 N + NOTNOMPREP) ;
#― Тогда я готов пригласить вас назавтра на breakfast, ― ответил он, весьма удивленный не то нашей осведомленностью, не то нахальством, не то с ног сшибающей непосредственностью. adj NOT selected ok

REMOVE:PTCP-SpecC-A8 A IF (0 Ptcp + $$NGDAIP + $$NBR)(0 A + $$NGDAIP + $$NBR)(NOT 0 Adv)(NOT 0 N)(-1 MeasureDegreeAdv LINK -1 Comm LINK -1 Pron + $$NGDAIP + $$NBR OR N + $$NGDAIP + $$NBR) (1 CC LINK 1 N + Ins OR Pron + Ins) ;
# postposition, adv of measure/degree, dependent-Ins
#Тогда я готов пригласить вас назавтра на breakfast, ― ответил он, весьма удивленный не то нашей осведомленностью, не то нахальством, не то с ног сшибающей непосредственностью.

REMOVE:PTCP-SpecC-A9 A IF (0 Ptcp)(0 A + $$NGDAIP)(NOT 0 Adv)(NOT 0 N)(1 Pr LINK 1 N LINK 1 N + $$NGDAIP) ;
#preposition
# ambg.ptcp + adjunct (PP) + head noun
#МКС угрожало столкновение с потерянным на орбите болтом.(does NOT work as the rule 467 replaces it. Without 467 it works.) ok
#Длинные, красивые, блестящие на солнце волосы - это мечта любой женщины. => блестящие
# (open corp) Словно эти слова не имели для него смысла, а смысл имели блуждающие сквозь тучи лучи осеннего солнца, поджигавшие на далёких лесах красные и жёлтые пятна. => блуждающие
# (open corp) А таковой является в первую очередь расположенный к северу Нигер , с которым Нигерия делит воинственную народность хаоса , известную своей приверженностью исламу . => расположенный

REMOVE:PTCP-SpecC-A10 A IF (0 Ptcp + $$NGDAIP + $$NBR)(0 A + $$NGDAIP + $$NBR)(NOT 0 Adv)(NOT 0 N)(-1 Comm LINK -1 N  + $$NGDAIP + $$NBR OR Pron + $$NGDAIP + $$NBR)(1 QuotPlain LINK 1 WORD) ;
#complement
#Тут цвет какого-то нового общества, ещё не устоявшегося, ещё кипящего в водовороте отбора, ещё в этом водовороте не выработались окончательные правила, говорящие "да" и "нет", но уже вознесённого на гребень успеха. => говорящие

REMOVE:PTCP-SpecC-A11 A IF (0 Ptcp)(0 A + $$NGDAIP + $$NBR)(NOT 0 Adv)(NOT 0 N)(-1 Comm LINK -1 N)(1 Adv OR Kak) ;
#postposition
#head noun + commm + ambg.ptcp + pp
# (open corp) Московская фонологическая школа вводит понятие гиперфонемы как единицы, выступающей лишь в тех случаях нейтрализации, для которых невозможно подобрать сильную позицию, то есть в изолированных слабых позициях, в частности в русск. сто (в фонемной транскрипции московских фонологов — &lt;{с/з}то&gt;), баран (&lt;б{а/о}ран&gt;). => выступающей
# (open corp) Однако же, самыми ярыми сторонниками «спасшейся княжны» стали Татьяна и Глеб Боткины, дети последнего лейб-медика двора, убитого вместе с царской семьёй. => убитого
#Учеников, мыслящих иначе, мы готовы немедленно занести в списки нерадивых. => мыслящих

REMOVE:PTCP-SpecC-A12 A IF (0 Ptcp)(0 A + $$NGDAIP + $$NBR)(NOT 0 Adv)(NOT 0 N)(-1 Pr LINK -1 V)(1 N + Dat LINK 1 N + Loc OR N + Prp) ;
# preposition
# prep + ambg.ptcp + n-Dative + head noun
#... когда большинство членов кибуца не работают на принадлежащих кибуцу предприятиях => принадлежащих

REMOVE:PTCP-SpecC-A13 A IF (0 Ptcp)(0 A)(NOT 0 Adv)(NOT 0 N)((-1 TempLocAdv LINK -1 Pr) OR (-1 TempLocAdv LINK -1 N LINK -1 Pr))(1 Col OR Dash) ;
#prep/prep.phrase (localization) + temp.adv + ambg.ptcp + punct (dash, colon)
#elliptical construction
#... новый реактор будет возведен на месте уже существующего - на юге Сиднея. => существующего
#... новый реактор будет возведен вместо уже существующего - на юге Сиднея. => существующего
#... новый реактор будет возведен вместо уже существующего: на юге Сиднея. => существующего

REMOVE:PTCP-SpecC-A14 A (0 Ptcp + Pred)(0 A + Pred)(NOT 0 Adv)(NOT 0 N)((-1 N) OR (-1 Ne LINK -1 N) OR (-1 TempLocAdv LINK -1 N)) ;
#predicative use of short participial forms
#А любимее всего – финал уже открыт. А любимее всего – финал открыт. => открыт
#А любимее всего – финал не открыт. => открыт
# (open corp) Москва же романтического ореола лишена. => лишена
# (open corp) « Она остановилась в доме моей племянницы, окружившей её вниманием и заботой(…) её ответное отношение к великой княгине оказалось таковым, что &lt;муж Ксении Георгиевны&gt; Уильям Лидс вынужден был выставить её прочь. » => вынужден
#Однако иностранным строителям вход на российский рынок доступного жилья закрыт . => закрыт

REMOVE:PTCP-SpecC-A15 A (0 A + $$NGDAIP + $$NBR)(0 Ptcp + $$NGDAIP + $$NBR)(NOT 0 Adv)(NOT 0 N)(-1 Pron + Gen LINK -1 Comm LINK -1 N + $$NGDAIP + $$NBR)(1 Comm) ;
#Рискну предположить, что люди, его лишенные, как раз наиболее отзывчивы на указания, когда нужно смеяться, как мой прежде малолетний сын. => лишенные

###############################################################################
#Rules for general contexts
###############################################################################

SECTION
#Section 3: rules for general contexts (+ 1 weighted rule)

#REMOVE A:

REMOVE:WPTCP-GenC-Prep A IF (0 A + (<W=MIN>))(0 Ptcp  + (<W=MAX>))(NOT 0 Adv)(NOT 0 N)(1 Pr) ;
#participle followed by a preposition

REMOVE:PTCP-GenC-V1 V IF (0 A + $$NGDAIP + $$NBR)(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(-1 N + $$NGDAIP + $$NBR)(1 Comm) ;
# человека говорящего, но => говорящего

REMOVE:PTCP-GenC-A1 A IF (0 Ptcp + $$NGDAIP + $$NBR)(0 A + $$NGDAIP + $$NBR)(NOT 0 Adv)(NOT 0 N)(-1 TempLocAdv)(1 N + $$NGDAIP + $$NBR) ;
#adverbs of time/place
# (rnc var) заранее заданными свойствами => заданными
#overlap with rule 750

REMOVE:PTCP-GenC-A2 A IF (0 Ptcp + Pred)(0 A + Pred)(NOT 0 Adv)(NOT 0 N)(1 Nom) ;
#predicative use, short form
#Выставке «15—58». Химкинская история. Фотографии. Документы», которая прослеживает историю протеста против кровавой трассы и политического насилия в Химках, обеспечен широкий резонанс. => обеспечен
#Мировую известность художникам Татьяне Арзамасовой, Льву Евзовичу и Евгению Святскому, по начальным буквам фамилий которых и назван коллектив (позже к художникам присоединился выдающийся фотограф Владимир Фридкес), принес скандальный «Исламский проект», в котором 100-метровый оплот американской независимости – статуя Свободы – облачается в паранджу, католический храм Саграда Фамилия вонзает в небо исламские полумесяцы, в Нью-Йорке горят небоскребы, а на Красной площади сидят арабские боевики с «калашами» наперевес. => назван
#Вместо новой книги о семье Андрея Синявского вышел фильм, в основу которого положены те самые письма писателя из тюрьмы своей жене — Марии Розановой. => положены
#В КрАО в советское время были посажены сосны и ели, которые закрывали прямой свет от зданий, поставлены низкие фонари для уменьшения рассеянного света, был посажен сад, который препятствовал появлению пыли в воздухе. => посажены, посажен

#SELECT A:

REMOVE:PTCP-GenC-A3 A IF (0 Ptcp)(0 A + $$NGDAIP)(NOT 0 Adv)(NOT 0 N)(-1 TempLocAdv)(1 N + $$NGDAIP) ;
# (rnc var) Робот, которого доставили вчера, был с заранее заданными свойствами. => заданными

SELECT:WPTCP-GenC-A4 A + $$NGDAIP IF (0 Ptcp)(0 A)(NOT 0 Adv)(NOT 0 N)(-1C Samyj + $$NGDAIP OR Adv/Cmpar OR Naibolee)(0 A OR V)(0 A + (<W=MIN>)) ; #from safe lexicalised operations
#combination with adverbs of measure and degree

SELECT:PTCP-GenC-A5.1 A IF (0 Ptcp)(0 A + $$NGDAIP/NBR)(NOT 0 Adv)(NOT 0 N)(-1 A + $$NGDAIP/NBR) ;
#synonymic row: adj ptcp
# (up) Это был хороший знакомый приятель => знакомый
# (rnc) Кажется, ни одна потеря так не била по душе, как неведомый убитый мальчик, раскинувший руки в пыльной траве. => убитый
# (rnc) В другой раз, гуляя с Ханной по степи, мы встретили странную кучку людей: впереди шел худой оборванный татарин, везя за собой маленькую тележку, в которой лежал крошечный ребенок. => оборванный
 # (rnc) Но, строго их осуждая, он втайне мечтает о новых молодых мстителях, которые с такой же жертвенностью обрушатся на настоящее, если не во имя будущего, то хотя бы с мечтой о возврате прошлого; за дымной завесой нынешнего пожара что прошлое уже рисуется ему прекрасным потерянным раем. => потерянным

SELECT:PTCP-GenC-A5.2 A IF (0 Ptcp)(0 A + $$NGDAIP/NBR)(NOT 0 Adv)(NOT 0 N)(1 A + $$NGDAIP/NBR) ;
#synonymic row: ptcp adj
# (rnc) И человек, в чьей голове произошло всё это дело, глядя на дифференциальные уравнения и на куски фотографической бумаги, подтверждавшие порождённую им истину, всхлипывал и вытирал плачущие счастливые глаза… => плачущие

SELECT:PTCP-GenC-A5.3 A IF (0 Ptcp)(0 A + $$NGDAIP/NBR)(NOT 0 Adv)(NOT 0 N)(1 Comm LINK 1 A + $$NGDAIP/NBR) ;
#synonymic row: adj , ptcp
# (rnc) И взволнованным, виноватым голосом Виктор Иванович вдруг прибавил: ― Виноват я перед тобой, Вера. Должен был раньше выйти в отставку…  => взволнованным
# (rnc)  В каждом взгляде, испуганном, ненавидящем, скрытном, таилась смерть. => испуганном
# (rnc) Все господа офицеры сидели в кают-компании, подавленные, удрученные и взволнованные и «позором» корвета, и адмиральским «ураганом», и, главное, судьбой этого отчаянного «Сереженьки», который решился назвать адмирала, да еще на шканцах, «бешеной собакой». => подавленные

SELECT:PTCP-GenC-A5.4 A IF (0 Ptcp + $$NGDAIP/NBR)(0 A + $$NGDAIP/NBR)(NOT 0 Adv)(NOT 0 N)(1 N + $$NGDAIP + $$NBR LINK 1 Sent) ;
# (up) Это был образованный человек. 
#overlap with rule SCPtcpV4 and SCPtcpV13
#2 true positives in syntagrus
 
 #########################Collocation selection (ptcp/adj n) #######################
 
SELECT:PTCP-GenC-A6.1 A IF (0 A + $$NGDAIP/NBR) (0 Ptcp + $$NGDAIP/NBR)(NOT 0 Adv)(NOT 0 N)(-1 Punct OR CS OR CC OR Pr)((1 N + $$NGDAIP/NBR) OR (1 N + $$NGDAIP/NBR LINK 1 A + Ins LINK 1 N + Ins)) ;
 #Для участников экономической жизни России с высоким уровнем роста цен на жилье и недвижимость, невысоким качеством промышленной продукции и постоянным увеличением стоимости экономических активов Латвии и Эстонии смотрятся как зазеркалье: заколдованный мир, в котором все стоит дешевле, чем должно – если бы только это было в России.  => заколдованный
#Если вы знаете что-то о людях, переехавших в бывшие страны Балтии или построивших там дачу, пишите нам в редакцию. =>  бывшие
#Бюджет края на следующий год уже пересмотрен, отметил Чиркунов: доходная часть снижена на 35%, половина этой суммы выпадает из-за снижения поступлений налога на прибыль.
#на следующий год заложить в бюджете 75% от этой суммы, а на 2011 год — 50%, чтобы у ВАЗа было время на серьезную перестройку; => следующий
#Следующий китайский пилотируемый космический полёт запланирован на следующий год. => следующий
#Имеются планы и задел на следующий год. => следующий
#Надеюсь на следующий год. => следующий
#Одновременно он дементировал сообщения о том, что якобы 7 сентября президент «Феррари» Лука ди Монтеземоло (Luca di Montezemolo) объявит о пилотах на следующий год. => следующий
#Съездили в Пермский край, на следующий год планируется Средний и Южный Урал. => следующий
#«Я работаю учителем семь лет» — говорит он — «и сильнее всего меня раздражают бюрократические ограничения на нововведения в духе „Да, конечно, мы заложим это в бюджет на следующий год“, даже если вы можете реализовать свою замечательную идею прямо сейчас».  => следующий
#Топ-менеджменту снизили зарплату на 20%, еще объявили об облегченной версии Lada Kalina стоимостью до 200 тыс. рублей. => облегченной
#Огромные размеры дореволюционных фабричных залов, захватывающий вид на Кремль и Пречистенскую набережную, а также островное местоположение делают «Красный Октябрь» уникальным местом в Москве. => захватывающий
 
SELECT:PTCP-GenC-A6.2 A IF (0 A)(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(-1 Pr)(1 N + $$NGDAIP/NBR LINK 1 Gen) ;
#participial phrase is part of prespositional phrase
#(opencorp)Чтобы помочь читателям, размышляющим над этими вопросами, мы начинаем рассчитывать и публиковать специальные показатели, измеряющие величину разрыва между текущей стоимостью самых популярных финансовых активов и оценкой, которую им мог бы дать наиболее скептический и консервативный аналитик.  => текущей
#(opencorp)Но что поразило меня более всего, так это сходство молодой женщины с вдовствующей императрицей. => вдовствующей

#SELECT N:

SELECT:PTCP-GenC-N1 N IF (0 N)(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(-1 Pr)(1 N + Gen) ;
# (open corp) Согласно данным журнала «Экономист», в результате глобального кредитного кризиса, ставшего результатом кризиса в сфере недвижимости, спад наметился во всех восточноевропейских странах. => данным
# (open corp) Японская полиция, по данным газеты, отметила, что в этом году число представителей радикальных организаций было несколько больше, чем в прошлые годы. => данным

#SELECT V:

SELECT:PTCP-GenC-V1.1 V IF (0 Ptcp)(0 A)(NOT 0 Adv)(NOT 0 N)(1 Pron + Ref) ;
#participles used with reflexive pronouns

SELECT:PTCP-GenC-V2 V IF (0 Ptcp)(0 A)(0C NOINS)(NOT 0 Adv)(NOT 0 N)(1 Ins) ;
# (rnc) «― спрашивает Дмитриев, все более и более удивленный и испуганный словами солдата. => испуганный
#(open corp)Очень показательно, что Венгрия (единственная на тот момент страна — член НАТО, имевшая с Югославией общую сухопутную границу) лишь допустила на свои аэродромы американские самолеты-заправщики и спасательные вертолеты (то есть вспомогательные, но не боевые силы), а также вытребовала эскадрилью американских истребителей для выполнения (это было специально подчеркнуто венгерскими официальными лицами) исключительно задач ПВО. => подчеркнуто
#(open corp)Впервые противопоставление фонемы и оттенка встречается у Л. В. Щербы в работе 1911 года «Court exposé de la prononciation russe», однако детально данное учение было развито автором в магистерской диссертации «Русские гласные в количественном и качественном отношении», вышедшей в свет в 1912 году. => развито
#(open corp)Дебилизм, Денис Викторович, в возлюбленных Вами дымящихся активных мозгах пресловутых 10% — дебилизм. => возлюбленных
#(open corp)Судебный процесс Анна Андерсон против Романовых тянулся до 1977 года, точнее, речь идёт о череде судебных процессов: не будучи удовлетворена результатами первого, закончившегося в 1961 году в Гамбурге, Андерсон потребовала новых разбирательств. => удовлетворена
#(open corp)« Да, я была вместе со всеми в ночь убийства, и, когда началась резня, я спряталась за спиной моей сестры Татьяны, которая была убита выстрелом. => убита
#(open corp) В то же время он сочетает в себе такое количество функций, что это позволяет утверждать: iPad — не уникальный технологический продукт, а хорошо продуманный и конъюнктурный, следующий основным трендам, которые будут определять медиа-потребление в течение ближайших 5—10 лет. => следующий
#previously  SELECT:GCPtcpV4 V IF (0 Ptcp)(0 A)(1 Ins) ;

SELECT:PTCP-GenC-V3 V IF (0 Ptcp-noPass + $$NOTACC )(0 A + $$NOTACC)(NOT 0 Adv)(NOT 0 N)(1C A + Acc OR N + Acc) ;
#complement
# (open corp) Наряду с авторами популярных интернет-дневников и малоизвестными, но вызывающими явную симпатию ведущими персонажами из круга профессионалов массмедиа в студии снова стали появляться народные любимцы. => вызывающий

SELECT:WPTCP-GenC-V4 V IF (0 Ptcp  + (<W=MAX>))(0 A + (<W=MIN>))(NOT 0 Adv)(NOT 0 N)(1 Dat) ;
#single use, preposed but still considered to be a participle
#(open corp) На фоне заявлений президента о необходимости помощи голодающим детям мира — и на фоне фото- и видеоматериалов на эту тему — проблемы усовершенствования антипригарного покрытия сковородок выглядят как инопланетное шоу, случайно пойманное антенной. => голодающим

SELECT:PTCP-GenC-V5 V IF (0 Ptcp)(0 A)(NOT 0 Adv)(NOT 0 N)(-1 Comm)(1 Nom OR Acc) ;
#postposition
#Скажите спасибо создателю, едкий сатирик, — закроют ночные клубы, у вас станет меньше мест, вызывающих возвышающую жёлчность и брезгливую неприязнь. => вызывающих
#Альбом начинается с протяжённой инструментальной преамбулы, звучащей восемь минут тридцать секунд, и только после неё следует текст песни «Shine On You Crazy Diamond». => звучащей
#Решение принято вслед за аналогичным решением о цензуре, принятым два месяца назад Высшим советом культурной революции Ирана, который возглавляет сам Ахмадинежад. => принятой

SELECT:PTCP-GenC-V6 V IF (0 Ptcp)(0 A)(NOT 0 Adv)(NOT 0 N)(1 Pr LINK 1 N) ;
#adjunct, prepostion phrase
#(open corp)Особую склонность избегать самостоятельных решений проявляют в японском деловом мире люди, только что повышенные в ранге. => повышенные
#(open corp)Но Анастасия в этот вечер чувствовала себя очень плохо и была — не более, впрочем, чем обычно — не расположена к разговорам: она повернулась спиной к принцессе и не отвечала ей ни слова. => расположена
#(open corp)Исключить реанимацию этой схемы нельзя , но , не говоря о её разрушительности для периферийных районов области , её техническая реализация чрезвычайно муторна и сопряжена с затратами , которые , надеюсь , будут с выражением озвучены в соответствующей сюжету речи министра Кудрина . => сопряжена
#(open corp)Мерное гудение установленной на стенде Xerox полноценной мини-типографии для печати книг по требованию казалось вполне мирным , а ритмичные звуки , издаваемые автоматическим книжным сканером , похожим одновременно на пыточные устройства маркиза де Сада и электрическую гильотину , погружали в печальные думы о судьбе книги . => установленной

SELECT:PTCP-GenC-V7 V IF (0 Ptcp)(0 A)(NOT 0 Adv)(NOT 0 N)(1 Pr) ;
#(open corp)Образованный в суровую военную пору, ваш институт был с самого начала ориентирован на решение самых важных, самых сложных проблем отечественной газовой индустрии. => образованный
#(open corp)Скандал, связанный с эпистолярным трехтомником, — скорее издательский. => связанный
#(open corp)Форум на сайте телеканала ТВ6, закрытого в 2002 году, еще довольно долго функционировал без какой-либо активности на сайте. => закрытого

SELECT:PTCP-GenC-V8 V IF (0 Ptcp + Pred)(0 A)(NOT 0 Adv)(NOT 0 N)(-1 Cop) ;
#predicative use
#(open corp)Она была убеждена, что врачи-евреи в клинике состоят в заговоре с большевиками и однажды они её предадут.»
#(open corp)Так и не добившись ни слова, посетители ушли, причём госпожа Толстая и её дочь были убеждены, что перед ними Великая Княжна Татьяна. => убеждены
#(open corp)Великий князь Андрей Владимирович, внук Александра II, впервые встретившийся с Андерсон в 1928 году, незадолго до её отъезда в США также был настроен весьма категорично: => настроен
#(open corp)Впервые противопоставление фонемы и оттенка встречается у Л. В. Щербы в работе 1911 года «Court exposé de la prononciation russe», однако детально данное учение было развито автором в магистерской диссертации «Русские гласные в количественном и качественном отношении», вышедшей в свет в 1912 году. => развито

SELECT:PTCP-GenC-V9 V IF (0 Ptcp)(0 A)(NOT 0 Adv)(NOT 0 N)(-1 TempLocAdv) ;
#this normally applies to wordforms used predicatively, temp.adv + ambg.ptcp
#(open corp)Лист много глубже, многограннее и философичнее, чем сейчас принято думать. => принято
#(open corp)Мы не сомневались в последствиях и полагали нового товарища уже убитым. => убитым
#(open corp)Гамлет здесь навсегда заворожён и заражён видом, ощущением смерти. => заворожён 
#(open corp)Походка ленивая бывает у женщин, сердце которых уже занято, — тут ты не пообедаешь. => занято
#formely rule GCPtcpV10

SELECT:PTCP-GenC-A1 A + $$NGDAIP IF (0 A)(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(1C* N + $$NGDAIP BARRIER Punct OR Pr OR Lparen OR NGDAIP - $$NGDAIP LINK -1C* A + $$NGDAIP BARRIER Punct OR Pr OR Lparen OR NGDAIP - $$NGDAIP) ;
#(opencorp)Движущей силой в поездах являются локомотивы, использующие электричество или производящие собственную мощность, обычно дизельными двигателями.
#(opencorp)При сохранении без изменений существующей социально-политической системы. => существующей
#it produces false results
#Fran and Rob's rule, it used to be on line 786

SELECT:PTCP-GenC-A2 A IF (0 A)(0 Ptcp)(NOT 0 Pred)(NOT 0 Adv)(NOT 0 N)(-1 MeasureDegreeAdv) ;
#wordforms combining with adverbs of measure and degree
#excludes predicative uses
# (rnc)Может быть, та подозрительная, вечно настороженная напряженность человека, у котораго дела в этом доме обстоят как-то очень неважно, и подозрительный нюх подсказывает в каждом углу притаившагося врага… => настороженная
#(opencorp)Более того, в качестве специального «бонуса» покупатели получат крайне озлобленных «аборигенов», лишившихся работы и смысла жизни, которые вполне могут начать портить «пришельцам» жизнь разными способами. => озлобленных
#(opencorp)В то же время он сочетает в себе такое количество функций, что это позволяет утверждать: iPad — не уникальный технологический продукт, а хорошо продуманный и конъюнктурный, следующий основным трендам, которые будут определять медиа-потребление в течение ближайших 5—10 лет.

###############################################################################
# Weighted rules
###############################################################################
SECTION # This is the final section, with an independent logic (remove non-max)
###############################################################################

#Adverbs of measure and degree:
REMOVE:WPTCP-V1.1 V IF (0 A  + (<W=MAX>))(0 Ptcp)(NOT 0 Pred)(NOT 0 Adv)(NOT 0 N)(-1 MeasureDegreeAdv)(1 N OR Pron LINK 1 N);
#wordforms combining with adverbs of measure and degree
#excludes predicative uses
# (rnc)Может быть, та подозрительная, вечно настороженная напряженность человека, у котораго дела в этом доме обстоят как-то очень неважно, и подозрительный нюх подсказывает в каждом углу притаившагося врага… => настороженная
#(opencorp)Более того, в качестве специального «бонуса» покупатели получат крайне озлобленных «аборигенов», лишившихся работы и смысла жизни, которые вполне могут начать портить «пришельцам» жизнь разными способами. => озлобленных
#(opencorp)В то же время он сочетает в себе такое количество функций, что это позволяет утверждать: iPad — не уникальный технологический продукт, а хорошо продуманный и конъюнктурный, следующий основным трендам, которые будут определять медиа-потребление в течение ближайших 5—10 лет.

SELECT:WPTCP-A1.2 A IF (0 A + (<W=MAX>))(0 Ptcp + (<W=MIN>))(NOT 0 Pred)(NOT 0 Adv)(NOT 0 N)(-1 MeasureDegreeAdv) ;

#Adverbs of comparative degree
SELECT:WPTCP-A1.3 A IF (0 A + (<W=MAX>))(0 Ptcp + (<W=MIN>))(NOT 0 Adv)(NOT 0 N)(-1 Adv/Cmpar) ;

#Adverbs of superlative degree
SELECT:WPTCP-A1.4 A IF (0 A + (<W=MAX>))(0 Ptcp + (<W=MIN>))(NOT 0 Adv)(NOT 0 N)(-1 Samyj OR Naibolee) ;

###################################Single use#######################################
#VP object:
SELECT:WPTCP-A2.1 A IF (0 A + (<W=MAX>))(0 Ptcp)(NOT 0 Adv)(NOT 0 N)((-1 V) OR (-1 Pron OR Det LINK -1 V))(1 N OR Pron) ;

#Александр Подусков, заместитель директора департамента управления по аренде компании KR Properties, занимающейся конверсией и владеющей всей бывшей фабрикой, объяснил решение по сносу дореволюционного здания тем, что корпус не был предусмотрен для длительного использования — стеклянные пирамидальные потолки, построенные так, чтобы максимальное количество света падало на руки ткачих, создавали протечки и разрушали саму конструкцию. => бывшей
#Позднее М. В. Панов указывал также, что в первой академической «Грамматике русского языка», над которой Щерба работал в последние годы жизни, предложена трактовка варьирования фонемы /а/, близкая к идеям МФШ; по мнению М. В. Панова, здесь отразилось изменение взглядов Л. В. Щербы, не успевшего соответствующим образом обновить другие положения в тексте грамматики. => соответствующим
#Относительно осознанно отмечается , наверное , только Новый год , потому и является самым любимым праздником => любимым
#Согласно одной из них, «Самоа» представляет собой сокращённую форму слова «Sa-ia-Moa», что переводится с самоанского языка как «священный для Моа». => сокращенную
#В результате получилась тонкая, отлично продуманная экспозиция, словно продолжающая уличную весеннюю панораму, где проснувшееся солнце уже начало могучую алхимическую работу по преображению предметов и людей. => продуманная

#PP object:
SELECT:WPTCP-A2.2 A IF (0 A + (<W=MAX>))(0 Ptcp)(NOT 0 Adv)(NOT 0 N)((-1 Pr) OR (-1 Det OR Pron LINK -1 Pr))(1 N OR Pron) ;
#В следующем году у предприятий прибыли почти не будет, говорит он, стоило бы передать регионам часть НДС, а не налога на прибыль. => следующем
#Проекты по перепрофилированию на Западе отличаются своей практичностью — депрессивные регионы с умирающей промышленностью так преображаются, что становятся местом притяжения туристов, пренебрегающих средневековыми замками и барочными дворцами ради атмосферы цехов, впечатлений от посещения шахт и газгольдеров. => умирающей
#Правда, мы получили в уходящем году «пророссийского» президента Украины, однако куда пойдёт Янукович в будущем — предсказать совершенно невозможно. => уходящем
#Проекты по перепрофилированию на Западе отличаются своей практичностью — депрессивные регионы с умирающей промышленностью так преображаются, что становятся местом притяжения туристов, пренебрегающих средневековыми замками и барочными дворцами ради атмосферы цехов, впечатлений от посещения шахт и газгольдеров. => умирающей

#NP object
SELECT:WPTCP-A2.3 A IF (0 A + (<W=MAX>))(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(-1 N)(1 N + Gen) ;
#учет повышенной ставки

#Single use, other:
SELECT:WPTCP-A2.4 A IF (0 A + (<W=MAX>))(0 Ptcp)(NOT 0 Adv)(NOT 0 N)((-1 N) OR (-1 Det OR Pron LINK -1 N))(1 N OR Pron) (NOT 1 Ins) ;
#Наряду с авторами популярных интернет-дневников и малоизвестными, но вызывающими явную симпатию ведущими персонажами из круга профессионалов массмедиа в студии снова стали появляться народные любимцы. => ведущими
#Публичный сектор, рассчитанный не только на малочисленную элиту, предусмотрен на «Красном Октябре», «Даниловской мануфактуре», «Красной Розе» и ещё на многих других бывших промзонах. => бывших

SELECT:WPTCP-A2.5 A IF (0 A + (<W=MAX>))(0 Ptcp)(0 NGDAIP/NBR)(NOT 0 Adv)(NOT 0 N)((-1 N) OR (-1 Det OR Pron LINK -1 N))(1 N + $$NGDAIP/NBR OR Pron + $$NGDAIP/NBR) ;
#Кроме того, в США был изменен дизайн обложки: если в английском варианте горящий человек наклонялся вперед в рукопожатии, то в американской версии он отклонялся назад. => горящий
#Белая тень авиалайнера сверкающим скальпелем вошла в мягкое, податливое подбрюшье дремлющего имперского зверя. => сверкающим

#Single use, beginning of a sentence:
SELECT:WPTCP-A2.6 A (0 A + (<W=MAX>))(0 Ptcp)(0 BOS)(NOT 0 Adv)(NOT 0 N)(1 N) ;
#Начинающие планеристы обязаны находиться в пределах границ зоны их домашнего аэродрома при самостоятельных полетах. => Начинающие планеристы

#############################Synonymic row#############################
#Synonymic row weighted:
SELECT:WPTCP-A5.1 A (0 A + (<W=MAX>))(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(1 N OR Pron)((-1 CC LINK -1 A LINK -1 Comm LINK -1 A) OR (-1 CC LINK -1 A LINK -1 Comm LINK -1 A LINK -1 Ne)) ;
SELECT:WPTCP-A5.2 A (0 A + (<W=MAX>))(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(1 N OR Pron)((-1 Comm LINK -1 A LINK -1 Comm LINK -1 A) OR (-1 Comm LINK -1 A LINK -1 Comm LINK -1 A LINK -1 Ne)) ;
SELECT:WPTCP-A5.3 A (0 A + (<W=MAX>))(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(1 N OR Pron)((-1 A LINK -1 Comm LINK -1 A) OR (-1 A LINK -1 Comm LINK -1 A LINK -1 Ne)) ;
SELECT:WPTCP-A5.4 A (0 A + (<W=MAX>))(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(1 N OR Pron)((-1 CC LINK -1 A) OR (-1 CC LINK -1 A LINK -1 Ne)) ;
SELECT:WPTCP-A5.5 A (0 A + (<W=MAX>))(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(1 N OR Pron)((-1 A) OR (-1 A LINK -1 Ne)) ;
SELECT:WPTCP-A5.6 A (0 A + (<W=MAX>))(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(1 N OR Pron)((-1 A LINK -1 A)) ;
SELECT:WPTCP-A5.7 A (0 A + (<W=MAX>))(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(1 S-BOUNDARY)((-1 Comm LINK -1 A) OR (-1 CC LINK -1 A)) ;


#Synonymic row unweighted:
SELECT:PTCP-A6.1 A (0 A)(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(1 N OR Pron)((-1 CC LINK -1 A LINK -1 Comm LINK -1 A) OR (-1 CC LINK -1 A LINK -1 Comm LINK -1 A LINK -1 Ne)) ;
SELECT:PTCP-A6.2 A (0 A)(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(1 N OR Pron)((-1 Comm LINK -1 A LINK -1 Comm LINK -1 A) OR (-1 Comm LINK -1 A LINK -1 Comm LINK -1 A LINK -1 Ne)) ;
SELECT:PTCP-A6.3 A (0 A)(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(1 N OR Pron)((-1 A LINK -1 Comm LINK -1 A) OR (-1 A LINK -1 Comm LINK -1 A LINK -1 Ne)) ;
SELECT:PTCP-A6.4 A (0 A)(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(1 N OR Pron)((-1 CC LINK -1 A) OR (-1 CC LINK -1 A LINK -1 Ne)) ;
#(yandex)и согласованной стратегии выхода
SELECT:PTCP-A6.5 A (0 A + (<W=MAX>))(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(1 N OR Pron)((-1 A) OR (-1 A LINK -1 Ne)) ;
SELECT:PTCP-A6.6 A (0 A + (<W=MAX>))(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(1 N OR Pron)((-1 A LINK -1 A)) ;
SELECT:PTCP-A6.7 A (0 A)(0 Ptcp)(NOT 0 Adv)(NOT 0 N)(1 S-BOUNDARY)((-1 Comm LINK -1 A) OR (-1 CC LINK -1 A)) ;
#Те, кто читал и почитал Хемингуэя, считался прогрессивным, продвинутым. => продвинутым

################################################################################
#LEVEL 2: morphological properties + some syntactic properties
################################################################################

#Predicative use:
#safe weighted rule
REMOVE:WPTCP-A-Pred A IF (0 A)(0 Ptcp + Pred + (<W=MAX>))(NOT 0 Adv)(NOT 0 N) ;
#unweighted rule
REMOVE:PTCP-A-Pred A IF (0 A)(0 Ptcp + Pred)(NOT 0 Adv)(NOT 0 N) ;

#negation particle
REMOVE:WPTCP-Ne-V A IF (0 A)(0 Ptcp + (<W=MIN>))(NOT 0 Adv)(NOT 0 N)(-1 Ne) ;

##################### + adjectivization => REMOVE V #####################
#Constraints are based on morphological properties (+ some context) discouraging participial uses 

#Composite patterns: + adjectivization

#active present adjectivized participles:
REMOVE:WPTCP-V4.1 V IF (0 A)(0 Ptcp + PrsAct + IV + (<W=MIN>))(NOT 0 Adv)(NOT 0 N)((NOT 1 S-BOUNDARY) OR (NOT 1 N + Dat) OR (NOT 0 Pr - Pr/Iz)) ;
REMOVE:WPTCP-V4.2 V IF (0 A)(0 Ptcp + PrsAct + Impf + (<W=MIN>))(NOT 0 Adv)(NOT 0 N)((NOT 1 S-BOUNDARY) OR (NOT 1 N + Dat) OR (NOT 0 Pr - Pr/Iz)) ;
#скучающий

#active past adjectivized participles without complement: e.g.: отживший, застывший:
REMOVE:WPTCP-V5.1 V IF (0 A)(0 Ptcp + PstAct  + V/PstActPerf + (<W=MAX>))(NOT 0 Adv)(NOT 0 N) ;
#suffixes -вш-, -ш-
REMOVE:WPTCP-V5.2 V IF (0 A)(0 Ptcp + PstAct + Perf + (<W=MIN>))(NOT 0 Adv)(NOT 0 N) ;
REMOVE:WPTCP-V5.3 V IF (0 A)(0 Ptcp + PstAct + Perf + IV + (<W=MIN>))(NOT 0 Adv)(NOT 0 N) ;

#passive past adjectivized participles with complement: 
REMOVE:WPTCP-V6.1 V IF (0 A)(0 Ptcp + PstPss + Perf + (<W=MIN>))(NOT 0 Adv)(NOT 0 N)(1 N + NOINS OR Pron + NOINS) ;
#уважаемый, воображаемый

#present passive adjectivized participles without complement: 
REMOVE:WPTCP-V6.2 V IF (0 A)(NOT 0 Adv)(NOT 0 N)(0 Ptcp + PrsPss + Perf + (<W=MIN>)) ;
#уважаемый, воображаемый

#passive past adjectivized participles without complement (non-predicative use): 
#perfective use
REMOVE:WPTCP-V7.2 V IF (0 A)(0 Ptcp + PstPss + Perf - Pred + (<W=MIN>))(NOT 0 Adv)(NOT 0 N) ;
#use with suffixes -нн/т-
REMOVE:WPTCP-V7.3 V IF (0 A)(0 Ptcp + PstPss + Perf + V/PstPssPerf - Pred + (<W=MIN>))(NOT 0 Adv)(NOT 0 N) ;
#single use
REMOVE:WPTCP-V7.1 V IF (0 A)(0 Ptcp + PstPss - Pred + (<W=MIN>))(NOT 0 Adv)(NOT 0 N) ;

##################### - adjectivization => REMOVE A #####################
#Constraints are based on morphological properties (+ some context) discouraging adjectival uses (-adjectivization)

##################### - adjectivization: composite patterns ##################

#active present participles:
#with suffix -ся-
REMOVE:WPTCP-A2.1 A IF (0 A)(0 PresActv + V/Ref + (<W=MAX>))(NOT 0 Adv)(NOT 0 N) ;
#transitive use
REMOVE:WPTCP-A2.2 A IF (0 A)(0 PresActv + TV + (<W=MAX>))(NOT 0 Adv)(NOT 0 N) ;

#active past participles:
#suffix -ну-
REMOVE:WPTCP-A3.1 A IF (0 A)(0 Ptcp + PastActv + V/PstActPerf-Nu + (<W=MAX>))(NOT 0 Adv)(NOT 0 N) ;
#transitive imperfective use
REMOVE:WPTCP-A3.2 A IF (0 A)(0 Ptcp + PstAct + Impf + (<W=MAX>))(NOT 0 Adv)(NOT 0 N) ;

#passive present participles with complement:
#imperfective transitive
REMOVE:WPTCP-A4.1 A IF (0 A)(0 Ptcp + PrsPss + Impf + TV + (<W=MAX>))(NOT 0 Adv)(NOT 0 N)(1 N + Ins OR Pron + Ins)  ; 
#imperfective 
REMOVE:WPTCP-A4.2 A IF (0 A)(0 Ptcp + PrsPss + Impf + (<W=MAX>))(NOT 0 Adv)(NOT 0 N)(1 N + Ins OR Pron + Ins)  ; 
#transitive
REMOVE:WPTCP-A4.3 A IF (0 A)(0 Ptcp + PrsPss + TV + (<W=MAX>))(NOT 0 Adv)(NOT 0 N)(1 N + Ins OR Pron + Ins)  ;
#suffix -мый-
REMOVE:WPTCP-A4.4 A IF (0 A)(0 Ptcp + PrsPss + V/PssPrs + (<W=MAX>))(NOT 0 Adv)(NOT 0 N)(1 N + Ins OR Pron + Ins)  ;
#general use
REMOVE:WPTCP-A4.5 A IF (0 A)(0 Ptcp + PrsPss + (<W=MAX>))(NOT 0 Adv)(NOT 0 N)(1 N + Ins OR Pron + Ins)  ; 

#passive present participles without complement:
#imperfective transitive
REMOVE:WPTCP-A4.6 A IF (0 A)(0 Ptcp + PrsPss + Impf + TV + (<W=MAX>))(NOT 0 Adv)(NOT 0 N) ; 
#imperfective 
REMOVE:WPTCP-A4.7 A IF (0 A)(0 Ptcp + PrsPss + Impf + (<W=MAX>))(NOT 0 Adv)(NOT 0 N) ; 
#transitive
REMOVE:WPTCP-A4.8 A IF (0 A)(0 Ptcp + PrsPss + TV + (<W=MAX>))(NOT 0 Adv)(NOT 0 N) ;
#suffix -мый-
REMOVE:WPTCP-A4.9 A IF (0 A)(0 Ptcp + PrsPss + V/PssPrs + (<W=MAX>))(NOT 0 Adv)(NOT 0 N)(1 N + Ins OR Pron + Ins)  ;

##################### + adjectivization: simple patterns #####################

#passive voice
REMOVE:WPTCP-V8 V IF (0 A)(0 Ptcp + Pass  + (<W=MIN>))(NOT 0 Adv)(NOT 0 N)(NOT 0 Pred)(NOT 1 S-BOUNDARY) ;

#present tense
REMOVE:WPTCP-V9 V IF (0 A)(0 PrcPres + (<W=MIN>))(NOT 0 Adv)(NOT 0 N) ;

#perfective
REMOVE:WPTCP-V10 V IF (0 A)(0 Ptcp + Perf  + (<W=MIN>))(NOT 0 Adv)(NOT 0 N)(NOT 0 Pred) ;

#####################  - adjectivization: simple patterns #####################

#past tense
REMOVE:WPTCP-A5 A IF (0 A)(0 PrcPast + (<W=MAX>))(NOT 0 Adv)(NOT 0 N) ;

#imperfective aspect
REMOVE:WPTCP-A6 A IF (0 A)(0 Ptcp + Impf + (<W=MAX>))(NOT 0 Adv)(NOT 0 N) ;

#transitive use
REMOVE:WPTCP-A7 A IF (0 A)(0 Ptcp + TV + (<W=MAX>))(NOT 0 Adv)(NOT 0 N) ;

#active voice
REMOVE:WPTCP-A8 A IF (0 A)(0 Ptcp + Actv + (<W=MAX>))(NOT 0 Adv)(NOT 0 N) ;

###############################################################################
#Final weighted rule
###############################################################################

SELECT:maxweight (<W=MAX>) IF (0 A)(0 Ptcp);
# For now, we only do A vs. V, other rules may be added with more weights in the lexicon.

###############################################################################
# Failed rules
###############################################################################

#part of NP (possessive) 
#SELECT:NPV3 V IF (0 A)(0 (Ptcp <W=MAX>))(-1 N)(1 N + Gen OR Pron + Gen) ;
#SELECT:NPV4 V IF (0 (A + Gen <W=MAX>))(0 Ptcp + Gen)(-1 N)(1 N + Gen OR Pron + Gen) ;
#these rules do NOT work for now

# после/посол
# SELECT Nom + $$MFN IF (0C Prop) (0C Acc OR Nom) (NOT -1C* Nom) (NOT 1C* Nom) (NOT -1* TV) (NOT 1* TV) (-1C* Cop + $$MFN) ;

# SELECT Nom + $$MFN IF (0C Prop) (0C Acc OR Nom) (NOT -1C* Nom) (NOT 1C* Nom) (NOT -1* TV BARRIER CLB) (NOT 1* TV BARRIER CLB) (1C* Cop + $$MFN BARRIER CLB) ;

# REMOVE Acc IF (0 Nom OR Acc) (NOT -1* TV) (NOT -1C* Pr) (NOT 1* TV) ;
# REMOVE Acc IF (NOT -1* TV) (-1* IV LINK 0 Fin) (NOT 1* TV BARRIER Fin) ;

# REMOVE CS IF (0 CS/Čto) (NOT -1* V OR Pred) ;

# Нападающие выжигали огнем места, где могли спрятаться их жертвы: дома с труднодоступными помещениями, церкви, леса, перелески и т. п.

# Distinguishing between adj/participle: if it gets arguments, it's NOT an adjective.

# REMOVE Fin IF (-1C SafeGuio) ;
# Maybe just with начало

#SELECT Prop IF (-1 Quot) (0 Prop OR N) (1 Quot) ;
#! «Мамонт», которое по-гречески значит «материнский», ...

#моей as prn, if "из моей школы"
#REMOVE A IF (-1C Pr)(0 Pron OR A);
#(1) it also covers "человек с говорящей фамилией", because of "either or" condition "0 Pron OR A", (2) мой is NOT defined as an adj in the lexicon
